#pragma switch,if
# Aplikace bez menu - pouze s hlavním panelem
# (c) 2016-2018 Martin Šmídek <martin@smidek.eu>

panel p [0,0,*,*] { type:'main', _sys:'$'
  var do_go=1
  var last='home'  //'home'
  var history: array, top=-1
  use f: form _f [0,0,,]
  use w: form _w [0,30,,]
  proc onstart() { var gets:object
    gets.set(function('return Ezer.get;'));
    last.set(cconc(gets.page,gets.page,'home'));
    // zapisuj každý klik hned
    function('Ezer.sys.ezer.activity.touch_limit=1; return 1;');
    go_menu(last.get);
  }
  proc opravit(_typ,_id) { 
    eq('xclanek',_typ); EDITOR.Clanek(_id); refresh 
  | eq('kalendar',_typ); EDITOR.Kalendar(_id); refresh 
  }
  proc zmenit(_mid,_typ1,_id,_typ2) { 
    ask('menu_chng_elem',_mid,_typ1,_id,_typ2); refresh
  }
  proc pridat(_typ,_mid,_first) { 
    eq('xclanek',_typ); ask('menu_add_elem',_mid,_typ,_first); refresh 
  | eq('xkniha.elem',_typ); ask('menu_add_elem',_mid,_typ,_first); refresh 
  }
  proc zcizit(_typ,_pid,_mid) { 
    echo("kopírování článku setkani.part.id=",_pid);
    ask('menu_copy_elem',_typ,_pid,_mid,'aclanek'); refresh
  }
# ----------------------------------------------------------------------------==> emulace událostí
  var page: object // {html,edit,id}
  proc cms_go(ref) { echo("přepnutí webu na ",ref); do_go.get; go(ref) }
  proc cms_menu(ref) { echo("přepnutí dle menu ",ref); do_go.get; go_menu(ref) }
  proc refresh() { cms_go(last.get); }
  proc ask_page(a,b) {
    page.set(ask('page',a,b));
    w.page.set(page.get('html'));
//    f.editace.Init(page.get('edit'));
  }
  proc go(ref) { 
    echo('go ',ref);
    history.set(last.get);
    last.set(ref);
    f.verze.set(sys('ezer','version')); 
    ask_page(function("return Ezer.app_root;"),ref);
    function("return jump_fokus();");
  }
  proc cms_go_case(pid) { var y:object, ref:text
    [ has_skill('a'); f.clr.get; clear ];
    y.set(ask('pid2menu',pid));
    f.verze.set(sys('ezer','version')); 
    ask_page(function('mref',"history_push2(mref);return Ezer.app_root;",y.url),y.page);
    function("return jump_fokus();");
  }
  proc go_menu(ref) { echo('go_menu ',ref);
    history.set(last.get);
    last.set(ref);
    f.verze.set(sys('ezer','version')); 
    ask_page(function("return Ezer.app_root;"),ref);
  }
# ----------------------------------------------------------------------------==> control bar
  form _f [,,*,30] { css:'grey admin', style:'z-index:9999;position:fixed'
    proc hide_all() { CMS.hide; ADMIN.hide; REDAKCE.hide }
    button [80,5,,]  { skill:'r|r', title:"[fa-chevron-left]"    
      proc onclick() { hide_all; go(history.get) } }
    button [101,5,,] { skill:'r|r', title:"[fa-repeat]"          
      proc onclick() { hide_all; cms_go(last.get) } }
    button [123,5,,] { skill:'r|r', title:"[fa-chevron-right]", format:'d'
      proc onclick() { hide_all } }
//    button editace [300,5,,] { 
//      proc Init(edt) {
//        eq('xclanek',edt); this.enable(1); this.set('[fa-edit] Editovat zobrazený článek')
//      | eq('kalendar',edt); this.enable(1); this.set('[fa-edit] Editovat kalendář')
//      | this.set('editace přes www.setkani.org'); this.enable(0)
//    }
//      proc onclick() { 
//        eq('xclanek',page.get('edit')); EDITOR.Clanek(page.get('id')); refresh 
//      | eq('kalendar',page.get('edit')); EDITOR.Kalendar(page.get('id')); refresh 
//    }}
    button [-95,5,,] { skill:'r|a', title:"[fa-gears] menu" 
      proc onclick() { hide_all; CMS.modal(10,40) } }
    label verze [-313,8,40,] { title:'ezer', style:'font-size:10pt;color:white' } 
    button [-238,5,,] { skill:'r|r', title:"[fa-file-text] přehledy" 
      proc onclick() { hide_all; ADMIN.modal(10,40) } }
    button [-160,5,,] { skill:'r|a', title:"[fa-user-circle] redakce" 
      proc onclick() { hide_all; REDAKCE.modal(10,40) } }
    button [-35,5,,] { title:"odhlásit", style:'z-index:999'
      proc onclick() { var ref:text
        //logout;
        ask('ask_server',°{cmd:'be_logout'});
        ref.set(conc(function("return Ezer.web.index"),"?page=",last.get));
        function('ref',"location.href= ref;",ref);
    } }
    button [-10,5,,] { skill:'r|r', title:"[fa-bars]"           proc onclick() { hide_all; } }
  }
# ----------------------------------------------------------------------------==> page
  form _w [,,*,*] { style:"position:initial"
    label page { css:'cms_page' }
    label mapa { type:'map', css:'cms_mapa' }
  }
}
# ----------------------------------------------------------------------------==> CMS
panel CMS [0,0,600,396] { type:'popup',  title:'CMS - definice menu', 
    style:"min-width:590px", _sys:'cms'
    css:'dialog', par:°{min_top:30}
  var web=2, from_table= 1
  use a: area a_ 
  use u: form _u [210,0,,] 
  proc onfirstfocus() { 
    a.menu;
    a.curr_id.set('menu.main menu');
    a.select_curr;
  }
  area a_ { 
    title:"<div id='tree' style='width:206px;height:400px;overflow:auto'></div>"
    var curr_id:text, curr_data:object // index a data nastaveného node
    menu { type:'context'                                                                skill:'a|m'
      // ----------------------------- nový článek
      item { title:'nový článek tohoto menu' proc onclick() { 
        ask('menu_add_elem',u.mid.get,'xclanek'); a.menu
      }}
      // ----------------------------- posuny
      item { title:'-posuň menu dolů' proc onclick() { 
        ask('menu_shift',u.mid.get,1); a.menu
      }}
      item { title:'posuň menu nahoru' proc onclick() { 
        ask('menu_shift',u.mid.get,0); a.menu
      }}
      item { title:'-vlož podmenu' proc onclick() { var root:object, root_id:text, node:object
        root.set(curr_data.get); root_id.set(curr_id.get);
        echo('root ',curr_id.get,debug(curr_data.get));
        // vkládat lze buďto nové top|main menu nebo submenu pod uložený root (s definovaným mid)
        or(eq(curr_id.get,'menu.top menu','menu.main menu'),root.mid);
        node.set(a.tree_insert(curr_id.get));
        curr_id.set(node.id); 
        a.tree_select(curr_id.get);
        curr_data.set(object('wid',web.get,
            'mid',0,
            'mid_top',root.mid,
            'typ',cconc(eq(root_id,'menu.top menu'),0,eq(root_id,'menu.main menu'),1,2),
            'level',root.level
        ));
        a.tree_update(curr_id.get,0,curr_data.get);
        copy_by_name(curr_data.get,u);
        echo('new ',curr_id.get,debug(curr_data.get));
      | warning("napřed je třeba menu uložit do tabulky")
      }}
      item { title:'odstraň menu' proc onclick() { 
        curr_id.set(a.tree_remove(curr_id.get)) 
      }}
    }
    proc select_curr() {
      a.tree_select(curr_id.get);
    }
    proc menu() { var obj: object
      obj.set(ask('menu_tree',web.get));
      this.tree_show(obj,'tree') 
    }
    // dat=node.data tzn. vše z tx_gnmenu
    proc tree_onclick(fid,id,dat,com,x,txt,txts) { var i:text
      u.init; 
      { dat; 
        curr_id.set(fid); curr_data.set(dat);
        copy_by_name(dat,u);
      | u.enable(0)
      }
    }
  }
  form _u [,,400,400] { css:'popup-right'
    label save_info [5,130,578,]  
    button [10,10,,] { title:"[fa-play]", help:"zobraz 2 úrovně menu"
      proc onclick() { a.tree_expand(2); 
    }}
    button [30,10,,] { title:"[fa-forward]", help:"zobraz všechny úrovně menu"
      proc onclick() { a.tree_expand(99); 
    }}
    button [60,10,,] { title:"[fa-database] Ulož do tabulky"                             skill:'a|m'
      proc onclick() {
        save_info.set(ask('menu_save',web.get,a.tree_dump));
        u.plain;
        a.menu;
        p.refresh;
    }}
    button [178,10,,] { title:"[fa-undo] Zpět"                                           skill:'a|m'
      proc onclick() {
        a.menu;
        p.refresh
    }}
    button [238,10,,] { title:"[fa-undo] Vrať předchozí menu"                            skill:'a|m'
      proc onclick() {
        ask('menu_undo',web.get);
        a.menu;
        p.refresh
    }}
    field mid [5,164,35,] { title:"^mid" }
    field mid_top [49,164,35,] { title:"^top" }
    field mid_sub [94,164,35,] { title:"^sub" }
    field typ [153,164,22,17] { title:"^typ" }
    field rank [186,164,22,17] { title:"^rank" }
    field level [218,164,22,17] { title:"^level" }
    field ref [6,203,79,] { title:"^ref" }
    field nazev [97,203,146,] { title:"^název" }
    field title [256,203,127,] { title:"^title" }
    edit elem [5,242,378,60] { title:"^definice obsahu" }
    proc onchanged(elem) {
      echo(elem.id); a.curr_data.set(elem.get,elem.id); elem.plain;
    }
  }
}
# ----------------------------------------------------------------------------==> ADMIN
panel ADMIN [0,0,600,465] { title:'Aktivita CMS', type:'popup', css:'dialog_menu', 
    style:"min-width:590px", _sys:'adm'
  proc onfirstfocus() { m.o.d.click }
  menu m { type:'left', format:'f'
    menu o {title:'Změny obsahu',type:'group'
      item d {title:'Přehled změn dnes',      par:°{cmd:'obsah',days:1} }
      item   {title:'Přehled změn za týden',  par:°{cmd:'obsah',days:7} }
      item   {title:'Přehled změn za měsíc',  par:°{cmd:'obsah',days:30} }
      item   {title:'Přehled změn za rok',    par:°{cmd:'obsah',days:365} }
      item   {title:'Přehled uskutečněných změn', par:°{cmd:'obsah',days:99999} }
      proc onclick(i) {
        panel.display(2,'mu'); u.den.display(0);
        u.result.set(conc(
          "<div class='karta'>",i.owner.title,' - ',replace_fa(i.title),"</div>",
          ask('log_report',i.par),"</div>"));
      }
    }
    menu a {title:'Změny aplikace',type:'group'
      item   {title:'[fa-exclamation] změna aplikace' 
        proc onclick(i) {
          u.result.set(conc(
            "<div class='karta'>",i.owner.title,' - ',replace_fa(i.title),"</div>"));
          panel.display(2,'ma');
          panel.call('msg_a.start');
        }
      }
      item d {title:'Přehled změn za měsíc', par:°{days:30} }
      item   {title:'Přehled změn za rok', par:°{days:365} }
      item   {title:'Přehled uskutečněných změn', par:°{days:9999} }
      proc onclick(i) {
        panel.display(2,'mu'); u.den.display(0);
        u.result.set(conc(
          '<br><div class="karta">Změny aplikace za posledních ',i.par.days,' dní</div><br>',
          ask('doc_chngs_show','a',i.par.days)));
      }
    }
    menu { title:'Aktivní redaktoři',type:'group',skill:'a'
      item { title:'dnes'               par:°{s:'uzivatele',c:'dnes',short:'1'}}
      item { title:'včera'              par:°{s:'uzivatele',c:'vcera',short:'1'}}
      item { title:'týden'              par:°{s:'uzivatele',c:'dny',days:'8',short:'1'}}
      item { title:'dva týdny'          par:°{s:'uzivatele',c:'dny',days:'15',short:'1'}}
      item { title:'měsíc'              par:°{s:'uzivatele',c:'dny',days:'30',short:'1'}}
      item { title:'nastavený den'      par:°{s:'uzivatele',c:'den',short:'1'}}
    }
    menu err { title:'Chyby',type:'group',skill:'a'
      item today { title:'dnes'         par:°{s:'chyby',c:'dnes'}}
      item { title:'včera'              par:°{s:'chyby',c:'vcera'}}
      item { title:'týden'              par:°{s:'chyby',c:'tyden'}}
      item { title:'měsíc'              par:°{s:'chyby',c:'mesic'}}
      item { title:'nastavený den'      par:°{s:'chyby',c:'den'}}
      item { title:'všechna hlášení'    par:°{s:'chyby',c:'vsechny'}}
      item { title:'čekající BUGs'      par:°{s:'chyby',c:'BUG1'}}
      item { title:'vyřešené BUGs'      par:°{s:'chyby',c:'BUG2'}}
    }
    menu p { title:'Přihlášení - redakce',type:'group'
      item d { title:'dnes'             par:°{s:'login',c:'dnes'}}
      item { title:'včera'              par:°{s:'login',c:'vcera'}}
      item { title:'týden'              par:°{s:'login',c:'tyden'}}
      item { title:'měsíc'              par:°{s:'login',c:'mesic'}}
      item { title:'nastavený den'      par:°{s:'login',c:'den'}}
      item { title:'všechna hlášení'    par:°{s:'login',c:'vsechny'}}
    }
    menu c { title:'Přihlášení - chlapi',type:'group',skill:'a'
      item c { title:'chybná přihlášení'   par:°{cmd:'me_login',typ:'ko'}}
      item   { title:'správná přihlášení'  par:°{cmd:'me_login',typ:'ok'}}
      proc onclick (i) {  var y:text
        panel.display(2,'mu'); u.den.display(0);
        y.set(ask('log_report',i.par));
        u.result.set(conc(
          "<div class='karta'>",i.owner.title,' - ',replace_fa(i.title),"</div>",
          y,"</div>"));
      }
    }
    proc onclick (i) {  var y:text
      panel.display(2,'mu'); u.den.display(1);
      y.set(ask('sys_activity',i.par,skip.get,u.den.get,sys('options','watch_access_opt')));
      u.result.set(conc("<div id=''>",y,"</div>"));
    }
  }
  use u: form _touch [210,0,,] { tag:'mu' }
  use msg_a: form _msg_a [210,0,,] { tag:'ma', format:'n' }
  var skip=0
  proc the_formsave (f,b) {
    f.same
  | f.key; f.save; { b.browse_seek; f.load | f.init }
  | f.insert; f.load;
    b.raise('onrowclick',b.browse_seek(conc(f.id_key,'=',f.key)))
  }
  form _touch [,,*,*] { css:'popup-right'
    field den [-20,4,85,18] { type:'date', style:'z-index:2', format:'Rrt', title:'nastavený&nbsp;den' }
    label result [5,0,*,*] { title:'', style:'overflow:auto' }
  }
  # ------------------------------------------------------------------------ _msg_a
  form _msg_a [,,*,500] {
    var virgin= 1
    proc start() { virgin.get; refresh; virgin.set(0) }
    proc refresh() { s.browse_load("kind='v'") }
    view h: table _help

    // příkazy
    label  [12,5,145,30] { css:'ae_parm'}
    button [20,11,,] { title:"[fa-database] Nový", proc onclick() { var ver_max:number
        form.init(2);
        version.set(max(ask('root_svn',0),ask('root_svn',1),sys('options','curr_version')));
        // pokud je verze=0, asi není na serveru SVN, nastav verzi o 1 větší, než maximum
        [ eq(version.get,'NaN'); {
            s.browse_count;
            ver_max.set(ask('select','MAX(version)','_help',"kind='v'"));
            version.set(sum(1,ver_max))
            //version.set(sum(1,s.ver.get))
          | version.set(1)
        }];
        version.change;
        kind.change; datum.set(now); datum.change; topic.change; hlp.change; }}
    button [79,11,,] { title:"[fa-trash-o] Vymazat", proc onclick() {
      confirm("Opravdu vymazat zprávu ",s.topic.get,"?");
      _help.delete_record(conc("id_help=",s.id_help.get)); refresh }}

    label  [240,5,123,30] { css:'ae_parm'}
    button [250,11,,] { title:"[fa-save] Ulož",  proc onclick() { the_formsave(form,s) }}
    button [306,11,,] { title:"[fa-undo] Zpět", proc onclick() { form.key(s.id_help.get); form.load}}

    // nová změna
    field kind { data:h.kind, value:'v' }
    field datum [12,45,79,] { type:'date', data:h.datum, format:'r' }
    field version [98,45,41,] { data:h.version, format:'r' }
    field name  { data:h.name }
    field topic [152,45,60,] { data:h.topic, format:'' }
    field wrk [698,20,32,] { title:'pracnost:', data:h.work, format:'r:e', skill:'m|mw' }
    field hlp [225,45,505,] { data:h.help, format:'' }
    // seznam změn
    browse s [10,75,150,100] { rows:20, qry_rows:1
      show id_help { data:h.id_help }
      show [,,72,] { title:'datum', data:h.datum, format:'s-q*rt' }
      show ver [,,60,] { title:'rev.SVN', data:h.version, format:'s-q*rt' }
      show topic [,,70,] { title:'topic', data:h.topic, format:'sq*t' }
      show [,,489,] { title:'help', data:h.help, format:'sq*t' }
      proc onrowclick() {
        form.key(id_help.get); form.load;
      }
    }
  }
}
# ----------------------------------------------------------------------------==> REDAKCE
panel REDAKCE [0,0,840,510] {type:'popup', title:'Redakce', css:'dialog_full', 
    style:"min-width:830px", skill:'a|a', _sys:'red'

  const sys_users_list = 22
  const sys_users_skills = 16
  const sys_users_cmd = 476

  var last_par: object,
  var novy: number,
  var new_skill: text,
  var sel: text,

  use uzivatele: form users [210+10,0,,],
  use uzivatel: form user [10+430+180,0,,]
  use cmd: form butt [210+22,sys_users_cmd,,],
  use dskill: form desc_skills [10+430+180,120,,]

  proc onfirstfocus() {
    watch_css;
#   }
#   proc onstart () {
    last_par.set(see.all.actives.par);
    sel.set('use');
    refresh;
    uzivatel.get.display(sys('options','watch_access'),'ac');
    uzivatel.get.display(sys('options','watch_ip'),'ip');
    cmd.watch_key.display(sys('options','watch_key'));
  }
  # ==> ... uložení uživatele s kontrolou
  proc u_uloz () { var idu:number
    uzivatel.same;
  | uzivatel.kdo.set(sys('user','id_user')); uzivatel.kdo.change; uzivatel.kdy.set(now_sql(1));
    uzivatel.kdy.change;
    uzivatel.key;
    { u_test(uzivatel.key)
    | uzivatel.save; uzivatel.load;
      uzivatele.lst.browse_seek;
      cmd.get.enable(0,'c');
    }
  | not(uzivatel.key);
    { u_test(0)
    | // vytvoření uživatele - musí platit id_user=ezer_db2.id_osoba
      idu.set(prompt(
            "pro správnou funkci emailového přihlašování je třeba vyplnit ID osoby z Answeru",0));
      idu;
      uzivatel.id_user.set(idu); uzivatel.id_user.change;
      uzivatel.insert; novy.set(uzivatel.key); uzivatel.load;
      uzivatele.lst.browse_load; uzivatele.lst.raise('onrowclick',novy.get);
      cmd.get.enable(0,'c');
    }
  }
  # test položek před uložením
  var msg:text
  proc u_test(id) {
    sys('options','watch_access');
    msg.set(ask('sys_user_access',sys('options','watch_access'),uzivatel.org.key,uzivatel.access.key));
    msg.get; alert(msg.get); return(1)
  | msg.set(ask('sys_user_unique',id,uzivatel.username.get,uzivatel.znacka.get));
    msg.get; alert(msg.get); return(1)
  | // testy prošly - uprav změny pro uložení
    [ uzivatel.access.changed; uzivatel.access.set(uzivatel.access.key); uzivatel.access.change ];
    [ uzivatel.org.changed; uzivatel.org.set(uzivatel.org.key); uzivatel.org.change ];
    [ uzivatel.password.changed; msg.set(ask('sys_hash_password', uzivatel.password.get)); uzivatel.password.set(msg.get); uzivatel.password.change ];
    return(0)
  }
  # obnovení seznamu uživatelů
  proc refresh() { var see_access:text
    see_access.set(cconc(sys('options','watch_access_opt'),conc(" AND access&",sys('user','access')),""));
    uzivatele.lst.browse_load(conc(last_par.get('cond'),see_access));
    cmd.enable(1,last_par.get('on'));
    cmd.enable(0,last_par.get('off'));
    watch_css;
  }
  # kompozice obarvení z Ezer.options.watch_access_opt.css (je-li)
  proc watch_css() { var opt:object
    sys('options','watch_access_opt');
    opt.set(ask('sys_user_watch_css',sys('options','watch_access_opt')));
    uzivatele.lst.zkratka.set_attrib('css_cell',replace(opt.css_cell,'@','org'));
    uzivatele.lst.username.set_attrib('css_cell',replace(opt.css_cell,'@','access'));
    uzivatel.org.selects(opt.sel_org);
    uzivatel.access.selects(opt.sel_access);
  }
  # ----------------------------------------------------------------==> ... menu
  menu see { type:'left', active:*, format:'f-'
    menu all { title:'přehled',type:'group'
      item actives { title:'[fa-user-plus] aktivní uživatelé', par:°{on:'h',off:'u',cond:"LEFT(deleted,1)!='H'"}}
      item         { title:'[fa-user-times] skrytí uživatelé', par:°{on:'u',off:'h',cond:"deleted LIKE 'H%'"}}
      item         { title:'[fa-user] všichni uživatelé',      par:°{on:'h|u',off:'',cond:1}}
    }
    proc onclick (i) {
      last_par.set(i.par);
      refresh;
    }
  }
  # ----------------------------------------------------------------==> ... uživatelé
  form users [,,834,473] { css:'ae_work'
    view u: table _user
//    view t: table _track { join_type:'NONE', join:"ON abbr=kdo" }
    label [6,3,89,15] { title:'<h1>Uživatelé</h1>' }
    browse lst [10,37,0,54] { rows:sys_users_list, qry_rows:1, buf_rows:100, group_by:'abbr'
      show user [,,37,] { title:'č.', help:'jednoznačné číslo uživatele',
        data:u.id_user, format:'rsq=' }
      show zkratka [,,35,] { title:'značka', help:'jednoznačná třípísmenná zkratka',
        data:u.abbr, format:'sq*', css_cell:'' }
      show username [,,63,] { title:'uživatel', help:'jednoznačné přihlašovací jméno',
        data:u.username, format:'sq*' }
      show prijmeni [,,70,] { title:'příjmení', data:u.surname, format:'s+q*' }
      show jmeno [,,70,] { title:'jméno', data:u.forename, format:'sq*' }
      show org [,,0,] { title:'org.', data:u.org, format:'rtsq*' }
      show access [,,0,] { title:'přístup', data:u.access, format:'rtsq*' }
      show skills [,,70,] { title:'oprávnění', help:'seznam kódů funkčních oprávnění',
        data:u.skills, format:'utsq*'
        proc onsubmit() { this.save } }
      show r0 [,,0,] { title:'letos',   expr:"0", format:'rtsq*' }
      show r1 [,,0,] { title:'loni',    expr:"0", format:'rtsq*' }
      show r2 [,,0,] { title:'předloni',expr:"0", format:'rtsq*' }
      show rn [,,0,] { title:'dřív',    expr:"0", format:'rtsq*' }
//      proc track(on) { on;
//        t.set_attrib('join_type','LEFT');
//        r0.set_attrib('expr',"SUM(IF(YEAR(t.kdy)=YEAR(NOW()),1,0))");
//        r1.set_attrib('expr',"SUM(IF(YEAR(t.kdy)=YEAR(NOW())-1,1,0))");
//        r2.set_attrib('expr',"SUM(IF(YEAR(t.kdy)=YEAR(NOW())-2,1,0))");
//        rn.set_attrib('expr',"SUM(IF(YEAR(t.kdy)<YEAR(NOW())-2,1,0))");
//        show_track; refresh
//      | t.set_attrib('join_type','NONE');
//        r0.set_attrib('expr',"0"); r1.set_attrib('expr',"0");
//        r2.set_attrib('expr',"0"); rn.set_attrib('expr',"0")
//      }
      proc onclick () {
        r0.width; // po rocích zobraz organizaci
        r0.width(0); r1.width(0); r2.width(0); rn.width(0);
        org.width(19); access.width(29); skills.width(230)
      | org.width; // po organizaci zobraz skill
        org.width(0); access.width(0); skills.width(280)
      | // po skill zobraz roky
        show_track
      }
      proc show_track() {
        r0.width(70); r1.width(69); r2.width(69); rn.width(69);
        skills.width(0); org.width(0); access.width(0)
      }
      proc onrowclick () {
        new_skill.set('');
        cmd.get.enable(0,'c');
        uzivatel.load(user.get);
        uzivatel.org.key(org.get);
        uzivatel.access.key(access.get);
        uzivatel.znacka.enable(0);
        // zobrazení v seznamu vlastností jako vybraných
        dskill.missing.set(ask('sys_skills_test',uzivatel.skills.get));
        dskill.seznam.selected('set',ask('sys_skills2ids',uzivatel.skills.get));
        dskill.seznam.selected(sel.get);
        dskill.seznam.browse_load;
      }
    }
  }
  # ----------------------------------------------------------------==> ... user
  form user [,,433,125] { style:"z-index:2"
    view u: table _user,
    label [5,6,89,15] { title:'<h1>Uživatel</h1>' }
    field id_user { data:u.id_user }
    field skills [2,46,146,] { data:u.skills, format:'n' }
    field username [5,48,60,] { data:u.username, title:'^username' }
    field password [74,48,42,] { data:u.password, title:'^heslo', format:'d' }
    field znacka [124,48,34,] { data:u.abbr, title:'^zkratka' }
    select org [168,48,66,] { tag:'ac', data:u.org, format:'w', title:'^organizace' }
    select access [242,48,44,] { tag:'ac', data:u.access, format:'w', title:'^přístup k' }
    field prijmeni [5,92,152,] { data:u.surname, title:'^příjmení' }
    field jmeno [168,92,117,] { data:u.forename, title:'^jméno' }
    edit ips [298,47,127,64] { tag:'ip', data:u.ips, title:'^povolené IP adresy' }
    field login { data:u.login }
    field kdo { data:u.zmena_kdo }
    field kdy { data:u.zmena_kdy }
    proc onchanged() { cmd.get.enable(1,'c') }
  }
  # ----------------------------------------------------------------==> ... desc_skills
  form desc_skills [,,433,351] { 
    label [2,3,100,15] { title:'oprávnění' }
    label missing [60,3,360,15]
    browse seznam [2,20,180,198] { rows:sys_users_skills, qry_rows:1, buf_rows:100
      show id { data:_skill.id_skill }
      show abb [,,50,] { data:_skill.skill_abbr, title:'kód', format:'utsq*', skill:'a|m'
        proc onsubmit() { this.save } }
      show dsc [,,345,] { data:_skill.skill_desc, title:'popis oprávnění', format:'uts+q$', skill:'a|m'
        proc onsubmit() { this.save } }
      # ----------------------------------------------==> ... kontext.menu
      menu { type:'context'
//        item { title:'zobrazit roční aktivitu'
//          proc onclick() { uzivatele.lst.track(1) } }
        item { title:'odebrat všechna oprávnění'
          proc onclick() { seznam.selected("clear"); cmd.enable(1,'c'); } }
        item { title:'-přidat nový řádek', skill:'m|m'
          proc onclick() {
            eq(sel.get,'ignore');
            seznam.browse_seek(conc('id_skill=',
              _skill.insert_record(
                object('skill_abbr',conc(abb.get,'?'),'skill_desc',conc(dsc.get,'.?')))));
          | confirm('Pro přidávání musí seznam opravnění zobrazovat všechny položky. Přepnout?');
            seznam.selected("ignore"); sel.set('ignore'); seznam.browse_load
        } }
        item { title:'odebrat běžný řádek', skill:'m|m'
          proc onclick() { confirm('opravdu odebrat oprávnění ',abb.get,':',dsc.get,'?');
            _skill.delete_record(conc('id_skill=',id.get));
            seznam.browse_load } }
        item { title:'-zobrazit vybrané'
          proc onclick() { seznam.selected("use"); sel.set('use'); seznam.browse_load } }
        item { title:'zobrazit vše'
          proc onclick() { seznam.selected("ignore"); sel.set('ignore'); seznam.browse_load } }
        item { title:'-zkontrolovat konzistentnost'
          proc onclick() { alert(ask('sys_user_skills')) } }
        item { title:'exportovat přehledovou tabulku'
          proc onclick() { ask('sys_user_skills','skills');
            alert("soubor lze stáhnout <a href='docs/skills.xlsx'>zde</a>"); } }
        item { title:'nositelé nastaveného oprávnění'
          proc onclick() {
            uzivatele.lst.selected('clear');
            uzivatele.lst.selected('set',ask('sys_user_skilled',abb.get))
          } }
        item { title:'-zobrazit user.options'
          proc onclick() { alert(ask('sys_user_options',uzivatele.lst.user.get)) } }
//        item { title:'-zobrazit historii změn'
//          proc onclick() { Track.back_show('_user',uzivatele.lst.user.get) } }
      }
      proc onchoice() {
#         // pokud je přihlášený administrátor, zablokujeme mu možnost změnit si oprávnění
#         eq(uzivatele.lst.user.get,sys('user','id_user'));
#         contains(uzivatele.lst.user.get,sys('ezer','options','admins'));
#         warning('Administrátor si nemůže měnit svoje práva');
#       |
        uzivatel.skills.set(ask('sys_ids2skills',this.selected('get')));
        uzivatel.skills.change;
        cmd.get.enable(1,'c');
      }
    }
  }
  # ----------------------------------------------------------------==> ... cmd
  form butt [,,852,39] {
    # -------------------------------- úpravy uživatelů
    label  [0,6,361,30] { css:'ae_parm' }
    button zrus_clena [11,12,,] { tag:'h',type:'html', title:'[fa-plug fa-rotate-180 fa-red] Skrýt' skill:'a|m'
      proc onclick() { confirm("Opravdu znemožnit přihlášení uživateli ",uzivatel.username.get,'?');
        alert(ask('sys_user_hide',uzivatele.lst.user.get,1)); refresh
    }}
    button obnov_clena [71,12,,] { tag:'u', type:'html', title:'[fa-plug] Obnovit'                  skill:'a|m'
      proc onclick() { confirm("Opravdu obnovit uživatele ",uzivatel.username.get,'?');
        alert(ask('sys_user_hide',uzivatele.lst.user.get,0)); refresh
    }}
    button watch_key [195,12,,] { title:'Vygenerovat klíč', format:'n'                              skill:'a|m' 
      proc onclick() {
        confirm("Vygenerování nového klíče znemožní přístup současným uživatelům. Pokračovat?");
        watch_ref.set(ask('sys_watch_key'))
      }
    }
    label watch_ref [316,15,250,15]
    # -------------------------------- změny uživatele
    label  [436,6,292,30] { css:'ae_parm' }
    button uloz_clena [449,12,,] { tag:'c', type:'html', title:'[fa-save] Uložit'                   skill:'a|m'
      proc onclick () { u_uloz
    } }
    button zpet_clena [515,12,,] { tag:'c', type:'html', title:'[fa-undo] Zpět'                     skill:'a|m'
      proc onclick () {
        uzivatel.key; uzivatel.load;
        cmd.get.enable(0,'c');
    } }
    button vytvor_clena [651,12,,] { type:'html', title:'[fa-heart] Vytvořit'                       skill:'a|m'
      proc onclick () {
        cmd.get.enable(0,'c');
        dskill.seznam.selected('clear');
        uzivatele.lst.blur; uzivatel.init; uzivatel.kdo.set(sys('user','id_user'));
        uzivatel.kdy.set(now);
        uzivatel.znacka.enable(1);
    } }
  }
}
# ==========================================================================================> EDITOR
panel EDITOR [0,0,710,530] { type:'popup', style:"min-width:700px", _sys:'edi'
  var id:number,
      path:text
  use c: form _c [0,0,,] { tag:'c', format:'n' }
  use k: form _k [0,0,,] { tag:'k', format:'n' }
  proc Kalendar(_key) {
    id.set(_key);
    panel.display(2,'k'); 
    [ _key; k.load(_key) | k.init ];
    panel.set_attrib('title','Úprava položky kalendáře');
    panel.modal(10,40) 
  }
  proc Clanek(_key) { var lock:object
  [ id.set(_key);
    panel.display(2,'c'); 
    lock.set(ask('record_lock','xclanek',_key)); // selže, pokud již je editováno
    not(lock.kdo);
    c.load(_key); 
    path.set(conc('/inc/c/',_key));                  // cesta pro přílohy
    ask('session','man,inc_path',path.get);
    join_drop(c.obsah);
    panel.set_attrib('title','Úprava článku');
    panel.modal(10,40) 
  | lock.kdo;
    alert("článek právě upravuje redaktor ",lock.kdo)
  ]}
  proc join_drop(obsah) {
    UPLOAD.ckeditor.set(obsah);
    UPLOAD.path.set(path.get);
    UPLOAD.g.drop.Init;
    [ function('edit','drop',"edit.label_drop=drop; return 1;",obsah,UPLOAD.g.drop) ]
  }
  # -----------------------------------------------------------------------------==> edit kalendář
  # úprava kalendáře
  form _k [10,10,710,460] {
    button [0,5,,]  { title:"[fa-chevron-left]", help:'starší akce'
      proc onclick() { next_xakce(1); }}
    button [21,5,,] { title:"[fa-repeat]"          
      proc onclick() { form.load(id.get) } }
    button [43,5,,] { title:"[fa-chevron-right]", help:'novější akce'
      proc onclick() { next_xakce(-1); }}
    field [68,5,50,] { data:xakce.id_xakce, format:'o' }
    label [120,7,400,] { title:"<== nalezni akci, kterou chceš upravit nebo založ novou ==>", 
        format:'c', style:"font-weight:bold;color:darkgreen;background-color:#c0e2c2" }
    button  [-99,5,,] { title:'Nová akce', help:'vytvořit novou akci'
      proc onclick() { form.init; }}
    button  [-46,5,,] { title:'Uložit', help:'ukončit editor a uložit změny'
      proc onclick() { 
        form.same
      | form.key; form.save; form.load; panel.hide(1); 
      | form.insert; form.load; panel.hide(1); 
      }}
    button  [-1,5,,] { title:'Zpět', help:'ukončit editor bez uložení změn'
      proc onclick() { panel.hide(0); }}
    // položky akce
    field [0,40,85,] { type:'date', title:'^ode dne', data:xakce.datum_od }
    field [95,40,85,] { type:'date', title:'^do dne', data:xakce.datum_do }
    field [190,40,268,] { title:'^název', data:xakce.nazev }
    field [470,40,240,] { title:'^místo', data:xakce.misto }
    edit obsah [0,70,710,450] { type:'html', data:xakce.web_text, par:°{toolbar:'WEB'} }
    // funkce
    proc next_xakce(smer) { var y:object
      y.set(ask('next_xakce',id.get,smer)); y.msg; warning(y.msg)
    | id.set(y.id); form.load(y.id) 
    }
  }
  # -----------------------------------------------------------------------------==> edit clanek    
  # oprava textu článku
  form _c [10,10,710,460] {
    button  [-121,5,,] { title:'Uložit', help:'ukončit editor a uložit změny'
      proc onclick() { 
        form.save; 
        ask('record_unlock','xclanek',id.get);
        ask('log_obsah','u','c',id.get); 
        panel.hide(1); }}
    button  [-75,5,,] { title:'Zpět', help:'ukončit editor bez uložení změn'
      proc onclick() { 
        ask('record_unlock','xclanek',id.get);  
        panel.hide(0); }}
    button app [-3,4,,] { title:'[fa-files-o] Přílohy', help:'seznam souborů sdružených s textem'
      proc onclick() {
        UPLOAD.ckeditor.set(obsah);
        UPLOAD.path.set(path.get);
        UPLOAD.modal //(50,100)
      }
    }
    select  [-171,5,100,] { type:'map', title:'text uvidí', help:'zobrazí se jen pro ...'
        options:skill_level.skill_name, value:'0', data:xclanek.web_skill }
    edit obsah [0,40,712,480] { type:'html', data:xclanek.web_text, par:°{toolbar:'WEB'} }
  }
}
# ----------------------------------------------------------------------------==> UPLOAD
panel UPLOAD [0,0,400,130] { title:' Nahrát soubor', type:'popup', style:"min-width:390px", 
    css:'dialog', _sys:'upl'
  var ckeditor:object,
      path:text
  use g: form _g [10,0,,]
  proc onfocus() {
    g.drop.loaded
  }
  form _g [,,400,100] {
    label dlab [0,10,380,] {tag:'l_', title:'... použij [init] ' }
    label drop [0,27,380,100] {tag:'l_', type:'drop', 
        par:°{contextmenu:"-vložit obrázek;vložit odkaz"}
      proc ondrop(f) {                                                // možnost odmítnutí přenosu
        //gt(f.size,5242880); warning('odmítnuto ',f.name,' má více jak 5MB'); return(0)
        echo('dropped=',f.name);                                      // jméno nemusí být ASCII
        return(f.name);
      }
      proc onload(f) { echo("loaded=",f.name) } // po dokončení přenosu, jméno bude ASCII
      proc onerror(e) { // funkce onerror nesmí obsahovat asynchronní kód a lokální proměnné
        echo("drop error=",e.msg); return(1) // vrácení 0 způsobí standardní hlášení chyby
      }
      proc onmenu(op,name,ref) { var name2:text
        eq(op,'remove'); echo(ask('drop_unlink',path.get,name,'S:')); loaded
      | eq(op,'rename'); [
          name2.set(prompt2("zadej nové jméno pro ",name)); 
          name2;
          echo(ask('drop_rename',path.get,name,name2,'S:')); loaded
        ]
      | eq(op,'remove-all'); echo(ask('drop_unlink',path.get,'*','S:')); loaded
      | eq(op,'-vložit obrázek');
        { not(ckeditor.get); warning("to lze jen v prostředí otevřeného editoru");
        | [ function('t','n',
              "t.ckeditor.insertHtml('<img src=\"'+n+'\"/>');",
              ckeditor.get,conc(path.get,'/',name)) ];
        }
      | eq(op,'vložit odkaz');
        { not(ckeditor.get); warning("to lze jen v prostředí otevřeného editoru");
        | function('t',"return t.ckeditor.getSelection().getSelectedText();",ckeditor.get);
          [ function('t','n',
              "t.ckeditor.insertHtml('<a target=\"doc\" href=\"'+n+'\">'
               +t.ckeditor.getSelection().getNative()+'</a>');",
              ckeditor.get,conc(path.get,'/',name)) ];
          panel.hide(1)
        | panel.hide(0);
          alert("pro vložení odkazu na soubor je třeba napřed označit text, na který se má klikat")
        }
      }
      proc Init() { drop.init(path.get,'S:'); }
      proc loaded() {
        Init;
        dlab.set(conc('soubory pro článek (',path.get,')'));
        echo('lsdir=',drop.lsdir)
      }
    }
    button [-14,2,,] { title:"[fa-cloud-download] Vložit soubor odkazem"
      proc onclick() { var url:text, ret:object
        url.set(UPLOAD_URL.modal);
        url; ret.set(ask('upload_url',url,EDITOR.id.get));
        ret.err; alert(ret.err)
      | url; g.drop.loaded
    }}
  }
  panel UPLOAD_URL [,,300,60] { type:'popup' title:'zadej url s přílohou', style:"min-width:300px" 
    use ff:form _ff
    proc onfocus() { ff.zip.init; ff.zip.focus }
    form _ff {
      field zip [0,0,300,] { format:'t' }
      button [0,30,,] { title:"[fa-save] Přidej přílohu"
        proc onclick() { panel.close(zip.get) } }
      button [100,30,,] { title:"[fa-undo] Zpět"
        proc onclick() { panel.close(0) } }
    }
  }
}
# ----------------------------------------------------------------------------==> tabulky
table menu { key_id:'mid', db:'setkani'
  number mid { key:'primary' }
  number mid_top 
  number mid_sub
  number level
  number typ
  number rank
  text ref
  text nazev
}
table xakce { key_id:'id_xakce', db:'setkani'
  number id_xakce { key:'primary' }
  text nazev
  text misto
  date datum_od { sql_pipe:'sql_date1' }
  date datum_do { sql_pipe:'sql_date1' }
  number web_stav
  text web_menu
  text web_text
}
table xclanek { key_id:'id_xclanek', db:'setkani'
  number id_xclanek { key:'primary' }
  number web_skill
  text web_text
}
table _help {  key_id:'id_help'                     // tabulka _help je v databázi aplikace
  number id_help { key:'primary' }
  text kind, number version, date datum { sql_pipe:'sql_date1' },
  text topic, text name, text seen, text help, number work
}
table _user { key_id:'id_user', db:'ezer_system'
  number id_user
  text deleted,
  text abbr,
  text skills  { help:'seznam procedurálních oprávnění' }
  text org     { help:'příslušnost k organizaci' } // 2**n
  text access  { help:'povolený přístup k datům organizací' }  // součet povolených org
  text ips     { help:'čárkami oddělený seznam IP adres ze kterých se uživatel smí přihlásit' }
  text options { help:'privátní nastavení|ve formátu JSON' }
  text username,
  text password,
  number state,
  text forename,
  text surname,
  text login,
  text history,
  text sess_id,
  text sess_time,
  text sess_data,
  number zmena_kdo,
  date zmena_kdy {  }
}
table _skill { key_id:'id_skill', db:'ezer_system'
  number id_skill
  text skill_abbr
  text skill_desc
}
//table _track { key_id:'id_track'
//  number id_track { key:'primary' }
//  date kdy { help:'datum změny' }
//  text kdo { help:'pachatel' }
//  text kde { help:'tabulka' }
//  number klic { help:'klíč' }
//  text fld { help:'položka' }
//  text op { help:'operace' }
//  text old { help:'původní hodnota' }
//  text val { help:'původní hodnota' }
//}
table _cis { key_id:'id_cis'
  number id_cis
  text druh,
  text data,
  text zkratka,
  number poradi,
  text hodnota,
  text popis,
  text barva,
  text ikona
}
map skill_level: table _skill { where:"id_skill IN (0,8,16)", order:'id_skill', key_id:'id_skill' }


