#pragma switch,if
# Aplikace bez menu - pouze s hlavním panelem
# (c) 2016-2017 Martin Šmídek <martin@smidek.eu>

panel p [0,0,*,*] { type:'main'
  var do_go=1
  var last='home'  //'home'
  var history: array, top=-1
  use f: form _f [0,0,,]
  use w: form _w [0,30,,]
  proc onstart() { var gets:object
    gets.set(function('return Ezer.get;'));
    last.set(cconc(gets.page,gets.page,'home'));
//    function('return admin(0)');
    go_menu(last.get);
  }
# ----------------------------------------------------------------------------==> emulace událostí
  var page: object // {html,edit,id}
  proc cms_go(ref) { echo("přepnutí webu na ",ref); do_go.get; go(ref) }
  proc cms_menu(ref) { echo("přepnutí dle menu ",ref); do_go.get; go_menu(ref) }
  proc refresh() { cms_go(last.get); }
  proc go(ref) { 
    echo('go ',ref);
    history.set(last.get);
    last.set(ref);
    f.verze.set(sys('ezer','version')); 
    page.set(ask('page',function("return Ezer.app_root;"),ref));
    w.page.set(page.get('html'));
    function("return jump_fokus();");
  }
  proc cms_go_case(pid) { var y:object, ref:text
    [ has_skill('a'); f.clr.get; clear ];
    y.set(ask('pid2menu',pid));
    f.verze.set(sys('ezer','version')); 
    page.set(ask('page',function('mref',"history_push2(mref);return Ezer.app_root;",y.url),y.page));
    w.page.set(page.get('html'));
    function("return jump_fokus();");
  }
  proc go_menu(ref) { echo('go_menu ',ref);
    history.set(last.get);
    last.set(ref);
    f.verze.set(sys('ezer','version')); 
    page.set(ask('page',function("return Ezer.app_root;"),ref));
    w.page.set(page.get('html'));
  }
# ----------------------------------------------------------------------------==> control bar
  form _f [,,*,30] { css:'grey admin', style:'z-index:9999;position:sticky'
    proc hide_all() { CMS.hide }
    button [80,5,,]  { skill:'a|a', title:"[fa-chevron-left]"    
      proc onclick() { hide_all; go(history.get) } }
    button [101,5,,] { skill:'a|a', title:"[fa-repeat]"          
      proc onclick() { hide_all; cms_go(last.get) } }
    button [123,5,,] { skill:'a|a', title:"[fa-chevron-right]", format:'d'
      proc onclick() { hide_all } }
        button [300,5,,] { title:"[fa-edit] Editovat zobrazený článek"
      proc onclick() { 
        eq('xclanek',page.get('edit')); EDITOR.Clanek(page.get('id')); refresh 
    }}
    button [-95,5,,] { skill:'a|a', title:"[fa-gears] CMS" 
      proc onclick() { hide_all; CMS.modal } }
    label verze [-150,8,40,] { title:'ezer', style:'font-size:10pt;color:white' } 
    button [-35,5,,] { title:"odhlásit", style:'z-index:999'
      proc onclick() { var ref:text
        //logout;
        ask('server',°{cmd:'be_logout'});
        ref.set(conc(function("return Ezer.web.index"),"?page=",last.get));
        function('ref',"location.href= ref;",ref);
    } }
    button [-10,5,,] { skill:'a|a', title:"[fa-bars]"           proc onclick() { hide_all; } }
  }
# ----------------------------------------------------------------------------==> page
  form _w [,,*,*] { style:"position:initial"
    label page { css:'cms_page' }
    label mapa { type:'map', css:'cms_mapa' }
  }
}
# ----------------------------------------------------------------------------==> CMS
panel CMS [0,0,600,296] { type:'popup',  title:'CMS - definice menu', 
    css:'dialog', par:°{min_top:30}
  var web=2, from_table= 1
  use a: area a_
  use u: form _u [210,0,,] 
  proc onfirstfocus() { 
    a.menu;
    a.curr_id.set('menu.main menu');
    a.select_curr;
  }
  area a_ { 
    title:"<div id='tree' style='width:206px;height:300px;overflow:auto'></div>"
    var curr_id:text, curr_data:object // index a data nastaveného node
    menu { type:'context'
      // ----------------------------- nový článek
      item { title:'nový článek tohoto menu' proc onclick() { 
        ask('menu_add_elem',u.mid.get,'xclanek'); a.menu
      }}
      // ----------------------------- posuny
      item { title:'-posuň menu dolů' proc onclick() { 
        ask('menu_shift',u.mid.get,1); a.menu
      }}
      item { title:'posuň menu nahoru' proc onclick() { 
        ask('menu_shift',u.mid.get,0); a.menu
      }}
      item { title:'-vlož podmenu' proc onclick() { var root:object, root_id:text, node:object
        root.set(curr_data.get); root_id.set(curr_id.get);
        echo('root ',curr_id.get,debug(curr_data.get));
        // vkládat lze buďto nové top|main menu nebo submenu pod uložený root (s definovaným mid)
        or(eq(curr_id.get,'menu.top menu','menu.main menu'),root.mid);
        node.set(a.tree_insert(curr_id.get));
        curr_id.set(node.id); 
        a.tree_select(curr_id.get);
        curr_data.set(object('wid',web.get,
            'mid',0,
            'mid_top',root.mid,
            'typ',cconc(eq(root_id,'menu.top menu'),0,eq(root_id,'menu.main menu'),1,2),
            'level',root.level
        ));
        a.tree_update(curr_id.get,0,curr_data.get);
        copy_by_name(curr_data.get,u);
        echo('new ',curr_id.get,debug(curr_data.get));
      | warning("napřed je třeba menu uložit do tabulky")
      }}
      item { title:'odstraň menu' proc onclick() { 
        curr_id.set(a.tree_remove(curr_id.get)) 
      }}
    }
    proc select_curr() {
      a.tree_select(curr_id.get);
    }
    proc menu() { var obj: object
      obj.set(ask('menu_tree',web.get));
      this.tree_show(obj,'tree') 
    }
    // dat=node.data tzn. vše z tx_gnmenu
    proc tree_onclick(fid,id,dat,com,x,txt,txts) { var i:text
      u.init; 
      { dat; 
        curr_id.set(fid); curr_data.set(dat);
        copy_by_name(dat,u);
      | u.enable(0)
      }
    }
  }
  form _u [,,400,300] { css:'popup-right'
    label save_info [5,130,578,]  
    button [10,10,,] { title:"[fa-play]", help:"zobraz 2 úrovně menu"
      proc onclick() { a.tree_expand(2); 
    }}
    button [30,10,,] { title:"[fa-forward]", help:"zobraz všechny úrovně menu"
      proc onclick() { a.tree_expand(99); 
    }}
    button [60,10,,] { title:"[fa-database] Ulož do tabulky"
      proc onclick() {
        save_info.set(ask('menu_save',web.get,a.tree_dump));
        u.plain;
        a.menu;
        p.refresh;
    }}
    button [178,10,,] { title:"[fa-undo] Zpět"
      proc onclick() {
        a.menu;
        p.refresh
    }}
    button [238,10,,] { title:"[fa-undo] Vrať předchozí menu"
      proc onclick() {
        ask('menu_undo',web.get);
        a.menu;
        p.refresh
    }}
    field mid [5,164,35,] { title:"^mid" }
    field mid_top [49,164,35,] { title:"^top" }
    field mid_sub [94,164,35,] { title:"^sub" }
    field typ [153,164,22,17] { title:"^typ" }
    field rank [186,164,22,17] { title:"^rank" }
    field level [218,164,22,17] { title:"^level" }
    field ref [6,203,79,] { title:"^ref" }
    field nazev [97,203,146,] { title:"^název" }
    field title [256,203,127,] { title:"^title" }
    field elem [5,242,378,] { title:"^definice obsahu" }
    proc onchanged(elem) {
      echo(elem.id); a.curr_data.set(elem.get,elem.id); elem.plain;
    }
  }
}
# ==========================================================================================> EDITOR
panel EDITOR [0,0,710,530] { title:' Úprava článku na webu', type:'popup'
  var id:number,
      path:text
  use c: form _c [0,0,,] { tag:'c', format:'n' }
  proc Clanek(_key) {[
    id.set(_key);
    panel.display(2,'c'); 
    c.load(_key); 
    path.set(conc('/inc/c/',_key));                  // cesta pro přílohy
    ask('session','web,inc_path',path.get);
    join_drop(c.obsah);
    panel.modal(10,40) 
  ]}
  proc join_drop(obsah) {
    UPLOAD.ckeditor.set(obsah);
    UPLOAD.path.set(path.get);
    UPLOAD.g.drop.Init;
    [ function('edit','drop',"edit.label_drop=drop; return 1;",obsah,UPLOAD.g.drop) ]
  }
  # -----------------------------------------------------------------------------==> edit clanek    
  # oprava textu článku
  form _c [10,10,710,460] {
    button  [-121,5,,] { title:'Uložit', help:'ukončit editor a uložit změny'
      proc onclick() { form.save; panel.hide(1); }}
    button  [-75,5,,] { title:'Zpět', help:'ukončit editor bez uložení změn'
      proc onclick() { panel.hide(0); }}
    button app [-3,4,,] { title:'[fa-files-o] Přílohy', help:'seznam souborů sdružených s textem'
      proc onclick() {
        UPLOAD.ckeditor.set(obsah);
        UPLOAD.path.set(path.get);
        UPLOAD.modal //(50,100)
      }
    }
    edit obsah [0,40,710,480] { type:'html', data:xclanek.web_text, par:°{toolbar:'WEB'} }
  }
}
# ----------------------------------------------------------------------------==> UPLOAD
panel UPLOAD [0,0,400,130] { title:' Nahrát soubor', type:'popup', css:'dialog'
  var ckeditor:object,
      path:text
  use g: form _g [10,0,,]
  proc onfocus() {
    g.drop.loaded
  }
  form _g [,,400,100] {
    label dlab [0,10,380,] {tag:'l_', title:'... použij [init] ' }
    label drop [0,27,380,100] {tag:'l_', type:'drop', 
        par:°{contextmenu:"-vložit obrázek;vložit odkaz"}
      proc ondrop(f) {                                                // možnost odmítnutí přenosu
        //gt(f.size,5242880); warning('odmítnuto ',f.name,' má více jak 5MB'); return(0)
        echo('dropped=',f.name);                                      // jméno nemusí být ASCII
        return(f.name);
      }
      proc onload(f) { echo("loaded=",f.name) } // po dokončení přenosu, jméno bude ASCII
      proc onerror(e) { // funkce onerror nesmí obsahovat asynchronní kód a lokální proměnné
        echo("drop error=",e.msg); return(1) // vrácení 0 způsobí standardní hlášení chyby
      }
      proc onmenu(op,name,ref) { var name2:text
        eq(op,'remove'); echo(ask('drop_unlink',path.get,name,'S:')); loaded
      | eq(op,'rename'); [
          name2.set(prompt2("zadej nové jméno pro ",name)); 
          name2;
          echo(ask('drop_rename',path.get,name,name2,'S:')); loaded
        ]
      | eq(op,'remove-all'); echo(ask('drop_unlink',path.get,'*','S:')); loaded
      | eq(op,'-vložit obrázek');
        { not(ckeditor.get); warning("to lze jen v prostředí otevřeného editoru");
        | [ function('t','n',
              "t.ckeditor.insertHtml('<img src=\"'+n+'\"/>');",
              ckeditor.get,conc(path.get,'/',name)) ];
        }
      | eq(op,'vložit odkaz');
        { not(ckeditor.get); warning("to lze jen v prostředí otevřeného editoru");
        | function('t',"return t.ckeditor.getSelection().getSelectedText();",ckeditor.get);
          [ function('t','n',
              "t.ckeditor.insertHtml('<a target=\"doc\" href=\"'+n+'\">'
               +t.ckeditor.getSelection().getNative()+'</a>');",
              ckeditor.get,conc(path.get,'/',name)) ];
          panel.hide(1)
        | panel.hide(0);
          alert("pro vložení odkazu na soubor je třeba napřed označit text, na který se má klikat")
        }
      }
      proc Init() { drop.init(path.get,'S:'); }
      proc loaded() {
        Init;
        dlab.set(conc('soubory pro článek (',path.get,')'));
        echo('lsdir=',drop.lsdir)
      }
    }
    button [-14,2,,] { title:"[fa-cloud-download] Vložit soubor odkazem"
      proc onclick() { var url:text, ret:object
        url.set(UPLOAD_URL.modal);
        url; ret.set(ask('upload_url',url,EDITOR.id.get));
        ret.err; alert(ret.err)
      | url; g.drop.loaded
    }}
  }
  panel UPLOAD_URL [,,300,60] { type:'popup' title:'zadej url s přílohou' use ff:form _ff
    proc onfocus() { ff.zip.init; ff.zip.focus }
    form _ff {
      field zip [0,0,300,] { format:'t' }
      button [0,30,,] { title:"[fa-save] Přidej přílohu"
        proc onclick() { panel.close(zip.get) } }
      button [100,30,,] { title:"[fa-undo] Zpět"
        proc onclick() { panel.close(0) } }
    }
  }
}
# ----------------------------------------------------------------------------==> tabulky
table menu { key_id:'mid', db:'setkani'
  number mid { key:'primary' }
  number mid_top 
  number mid_sub
  number level
  number typ
  number rank
  text ref
  text nazev
}
table xakce { key_id:'id_xakce', db:'setkani'
  number id_xakce { key:'primary' }
  text nazev
  text misto
  date datum_od
  date datum_do
  number web_stav
  text web_menu
  text web_text
}
table xclanek { key_id:'id_xclanek', db:'setkani'
  number id_xclanek { key:'primary' }
  text web_text
}
