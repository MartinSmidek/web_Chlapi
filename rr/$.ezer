menu rr { type:'main', active:rr.richard
  func onstart() { php.table_lock('none'); } // uvolni všechny zámky řádků tabulek přihlášeného 
  # ===================================================================================> RR
  tabs richard {title:"[fa-lightbulb-o] RR - myšlenky", _sys:'*', skill:'rr'
    panel rohr {type:'right', title:'[fa-send-o] Rozesílání'
      menu myslenka {type:'left'
        menu ukazat {title:'Zobrazení',type:'group'
          item dnesni {title:'[fa-arrow-down] dnešní myšlenky', par:{den:0,poslat:0} }
          item {title:'[fa-arrow-left] včerejší myšlenky', par:{den:-1,poslat:0} }
          item {title:'[fa-arrow-right] zítřejší myšlenky', par:{den:1,poslat:0} }
        }
        menu poslat {title:'Rozeslání',type:'group'
          item dnesni {title:'[fa-send-o] dnešní myšlenky', par:{den:0,poslat:1,opakovat:0} }
          item {title:' ... znovu', par:{den:0,poslat:1,opakovat:1} }
          item {title:' ... na martin@smidek.eu', 
                  par:{den:0,poslat:1,opakovat:0,test:'martin@smidek.eu'}, skill:'m'}
          item {title:'[fa-send] včerejší myšlenky', par:{den:-1,poslat:1,opakovat:0} }
          item {title:' ... znovu', par:{den:-1,poslat:1,opakovat:1} }
        }
        func onclick (i:ezer) {
          info.header(i);
          info.msg= '';
          info.fill('',php.rr_send(i.par));
        }
      }
      use info: form right [12,4,,]
    }
    panel plan {type:'right', title:'[fa-calendar] Kalendář a opravy'
      use f: form _f [12,4,,] { format:'n' }
      use g: form _g [12,4,,] { format:'n' }
      menu nastav {type:'left', format:'f'
        menu obdobi {title:'Opravy',type:'group'
          item od {title:'[fa-exclamation] Nastavení období'
            func onclick (i:ezer) { f.display(1); g.display(0); }
          }
          item {title:'[fa-times] Inicializace období', active:no
            func onclick (i:ezer) { 
              f.display(1); g.display(0); 
              if(confirm("opravdu chceš smazat všechny nastavené datumy a vymazat stav?")) {
                php.query("UPDATE rr SET datum='0000-00-00',state='' ");
                f.b.browse_refresh(1);
              }
          }}
          item {title:'... jen starší než dnešní', active:no
            func onclick (i:ezer) { 
              f.display(1); g.display(0); 
              if(confirm(`opravdu chceš smazat datumy a stav starší než dnešní?`)) {
                php.query("UPDATE rr SET datum='0000-00-00',state='' WHERE datum<NOW()");
                f.b.browse_refresh(1);
              }
          }}
          item {title:'[fa-edit] Oprava překladu'
            func onclick (i:ezer) { f.display(0); g.display(1); }
          }
        }
      }
      func onfirstfocus() {
        f.b.browse_load();
        g.b.browse_load()
      }
      # ---------------------------------------------------------------------------- opravy překladu
      form _g [,,*,100] {
        browse b [0,0,,] { rows:7, qry_rows:1, css_rows:'barva,1:yellow'
          show id_rr { data:rr.rr.id_rr, format:'qs' }
          show [,,40,] { title:'den', data:rr.rr.day, format:'qrs' }
          show [,,70,] { title:'datum', data:rr.rr.datum, format:'qs' }
          show [,,270,] { title:'název', data:rr.rr.subject, format:'qs' }
          show barva { expr:"IF(errata='',0,1)" }
          func onrowclick () { nacti() }
          func nacti() { g.load(id_rr) } }
        button [725,6,,] { title:'Dnes' 
          func onclick() {
            b.browse_seek(`datum='${now_sql()}'`);
            b.nacti()
        }}
        field czt [0,181,404,] { data:rr.rr.title_cz }
        field ent [422,181,401,] { data:rr.rr.title_en }
        button [778,6,,] { title:'Uložit' func onclick() { g.save(); g.load() } }
        edit cz [0,209,405,548] { type:'html', data:rr.rr.text_cz, par:{toolbar:'Minimal'} }
        edit en [421,209,405,548] { type:'html', data:rr.rr.text_en, par:{toolbar:'Minimal'} }
        label [423,19,,] { title:'Důvod pozměnění překladu' }
        edit errata [421,39,402,131] { data:rr.rr.errata }
      }
      # --------------------------------------------------------------------------- nastavení období
      form _f [,,*,500] {
        browse b [0,0,,] { rows:24, qry_rows:1
          show id_rr { data:rr.rr.id_rr, format:'qs' }
          show den [,,30,] { title:'pořadí', data:rr.rr.day_n, format:'rqs' }
          show [,,40,] { title:'den', data:rr.rr.day, format:'rqs' }
          show [,,20,] { title:'ps', data:rr.rr.state, format:'qs' }
          show datum [,,70,] { title:'datum', data:rr.rr.datum, format:'rqs' }
          show [,,140,] { title:'název', data:rr.rr.subject, format:'tqs' }
          func onsubmit() {
            form.den= den; form.datum= datum; } 
        }
        label [340,10,,] { title:"Nastav pořadové číslo dne, počáteční datum a počet dnů" }
        field den [340,40,30,] { format:'t'}
        field datum [380,40,90,] { type:'date', format:'rt' }
        field pocet [480,40,40,] { format:'t', value:'1' }
        button [530,40,,] { title:'nastav'
          func onclick() { var ret:object
            ret= php.rr_nastav(den,datum,pocet);
            msg= ret.msg;
            datum= replace(sql2date(ret.next),' ','');
            den= den + pocet;
            f.b.browse_seek(`day_n=${den-1}`);
        }}
        button [582,40,,] { title:'zruš'
          func onclick() { 
            msg= php.rr_zrus(den,pocet);
            f.b.browse_refresh();
        }}
        button [366,65,,] { title:'dnes^'
          func onclick() { datum= now(); datum.change(); }
        }
        button [418,65,,] { title:'^poslední'
          func onclick() { 
            datum= replace(sql2date(php.select('MAX(datum + INTERVAL 1 DAY)','rr')),' ',''); 
            datum.change(); 
        }}
        label msg [340,92,300,200]
      }
    }
  }
  # ===================================================================================> CAC
  tabs uvahy {title:"[fa-graduation-cap] CAC - meditace", _sys:'*', skill:'cac', include:'onload'}
  # ===================================================================================> Maily
  tabs maily {title:"[fa-send-o] Maily (new)", _sys:'*', skill:'maily'
    panel maily { type:'right', title:'Upomínací maily'
      use msg:  form right [12,4,,] { tag:'R' }
      use info: form _info [12,4,,] { tag:'E' }
      form _info [,,*,50] { style:'width:calc(100% - 24px)'
        // header
        label head [0,0,*,50]  { title:'' }
        func header(i:ezer) { var i_owner:ezer
          clear(); i_owner= i.owner(); 
          head= replace_fa(`<div class='karta'>${i_owner.title} - ${i.title}</div>`);
        }
        // mail
        field id_cron { data:cron.id_cron }
        label infox [10,55,622,70] 
        field info { data:cron.info }
        edit adr [10,155,800,100] { data:cron.adr, title:'^adresáti ve formátu JSON {skupina:seznam adres,...}' }
        field replyto [10,285,800,] { data:cron.replyto, title:'^odesílací adresa pro případ odpovědi' }
        edit pozn [10,330,800,150] { data:cron.par, 
            title:'^text nahrazující v dopisu {poznamka} ve formátu JSON {skupina:poznámka,...}, poznámky jsou ve formátu HTML' }
        edit txt [10,510,800,333] { data:cron.txt, title:'^text dopisu obsahující {poznamka}', type:'html' }
        // akce
        label [692,63,120,57] { css:'parm' }
        button [702,70,,] { title:'[fa-save] Uložení změn' 
          func onclick() { var ok:object
            ok= json_decode(adr);
            if (!ok) alert('zápis adresátů nemá validní formát JSON');
            ok= json_decode(pozn);
            if (!ok) alert('zápis poznámek nemá validní formát JSON');
            if (ok) {
              form.save(1); Load()
            }
        }}
        button [702,94,,] { title:'[fa-undo] Vrácení změn' func onclick() { Load()}}
        func Load() { form.load(1); infox= info; }
      }
      // menu
      func onfirstfocus() { m.p.i.click() }
      menu m {type:'left', format:'f', active:m.p.i
        menu p {title:'Pokladní upozornění',type:'group'
          item i {title:'[fa-edit] Úprava adresátů a textu'
            func onclick(i:ezer) { 
              panel.display(2,'E');
              info.header(i); clear();
              info.Load();
            }
          }
          item {title:'[fa-question] ukázat maily bez odeslání',par:{adresy:'*',poslat:0}}
          item {title:'[fa-send] poslat nyní všem upomínku',    par:{adresy:'*',poslat:1}}
          item {title:"[fa-send] ... JEN NA ADRESU 'ja' ",      par:{adresy:'ja',poslat:1}}
          func onclick(i:ezer) { 
            panel.display(2,'R');
            msg.header(i); clear();
            msg.fill('',php.note_send2(i.par.adresy,i.par.poslat));
          }
        }
        menu {title:'Log file',type:'group'
          item {title:'[fa-question] log odesílání mailů',  par:{typ:'mail',subj:'pokladna'} }
          func onclick (i:ezer) { 
            panel.display(2,'R');
            clear(); msg.header(i); 
            msg.fill('',php.stamp_show(i.par.typ,i.par.subj));
          }
        }
      }
    }
  }
  # ===================================================================================> Old Maily
  tabs oldmaily {title:"[fa-send-o] Maily", _sys:'*', skill:'mail'
    panel maily { type:'right', title:'Upomínací maily'
      use info: form right [12,4,,]
      menu m {type:'left', format:'f'
        menu p {title:'Editace a plán',type:'group'
          item {title:'[fa-cutlery] Text'
            func onclick(i:ezer) { 
              info.header(i); clear();
              info.fill('',php.note_text('ja'));
            }
          }
          item k {title:'[fa-calendar-check-o] Plán'
            func onclick(i:ezer) { 
              info.header(i); clear();
              info.append("rozesílá se 3. den v měsíci, je to třeba v Plánovači úloh na Synology");
            }
          }
        }
        menu poslat {title:'Rozeslání', type:'group', active:no
          item dnesni {title:'upomínky nyní všem', par:{adresy:'*'}}
          item {title:' ... na Miloše', par:{adresy:'mv'}, skill:'m'}
          item {title:' ... na martin@smidek.eu', par:{adresy:'ja'}, skill:'m'}
          func onclick(i:ezer) { 
            info.header(i); clear();
            info.fill('',php.note_send(i.par.adresy));
          }
        }
        menu {title:'Log file',type:'group', skill:'a'
          item {title:'[fa-question] odeslání mailu Pokladna',  par:{typ:'mail',subj:'pokladna'} }
          func onclick (i:ezer) { var cac:object
            clear(); info.header(i); panel.display(0,'.'); 
            info.fill('',php.stamp_show(i.par.typ,i.par.subj));
          }
        }
      }
    }
  }
  # ===================================================================================> Discord
  tabs discord { _sys:'*', skill:'discord'
      title:"<img src='rr/img/discord-B.png' style='float:left;height:12px;margin-right:5px'> Discord"
    panel maily { type:'right', title:'propojeni s Answerem'
      use info: form right [12,4,,] {
        field mail [100,60,200,] { title:"zadej mail:", format:'t' }
        button [350,60,,] { title:'patří iniciovanému chlapovi?' 
          func onclick() { clear(); answ= php.discord_mail(mail); }}
        label answ [100,90,400,50]
      }
      menu m {type:'left', format:'f', active:*
        menu p {title:'Ověření údajů',type:'group'
          item {title:'[fa-question] ověření mailu'
            func onclick(i:ezer) { 
              clear(); info.header(i); 
              info.answ= '';
            }
          }
        }
      }
    }
  }
  # ===================================================================================> Bans
  tabs bans {title:"[fa-exclamation] BANs", _sys:'*', skill:'ban'
    panel maily { type:'right', title:'Správa BANů v konferencích'
      use info: form right {
        select mail [10,40,150,] { type:'auto', par:{fce:'ban_maily_auto'}, format:'t'
          func onchanged() { var ido:number, name:text
            ido= this.key();
            if (ido) {
              name= php.select("CONCAT(jmeno,' ',prijmeni)",'ezer_db2.osoba',`id_osoba=${ido}`);
              inf= `${ido}: ${name} ${this.get()}`;
            }
            else {
              inf= `mail '${this.get()}' neznám`;
            }
          }
        }
        label inf [170,42,150,17]
      }
      menu m {type:'left', format:'f'
        menu p {title:'Přehled BANů',type:'group'
          item {title:'[fa-question] Hledání podle mailu'
            func onclick(i:ezer) { 
              info.header(i); clear(); 
              info.mail= ''; info.inf= '';
              info.fill('',' ');
            }
          }
        }
      }
    }
  }
  # ===================================================================================> Statistika
  tabs stat {title:"[fa-line-chart] Statistika", _sys:'*', skill:'brno'
    panel maily { type:'right', title:'Brněnská skupina'
      use info: form right [12,4,,]
      var last_par:object, last_menu:ezer
      func onfirstfocus() { info.Chart(1); }
      menu m {type:'left', format:'f'
        menu d {title:'Brněnská setkání',type:'group'
          item p {title:'[fa-question] Dělené skupiny'    par:{fce:'n_skupin',delena:1,confirm:0} }
          item   {title:'[fa-question] Společná setkání'  par:{fce:'n_skupin',delena:0,confirm:0} }
          item   {title:'[fa-question] Přehled chlapů (Answer zná)'    par:{fce:'survey-a',confirm:0} }
          item   {title:'[fa-question] Přehled chlapů (Answer nezná)'  par:{fce:'survey-n',confirm:0} }
          item   {title:'[fa-question] Přehled setkání'   par:{fce:'setkani',confirm:0} }
        }
        // fa-?-chart: area|bar|line|pie
        // graf=line|bar|bar%|pie, x=od-do, y=vek|pocet [,z=typ-ucasti]
        menu s2 {title:'Infografika',type:'group'
          item  {title:"[fa-line-chart] Stáří registrovaných účastníků", par:{
              graf:'spline/regression', yaxis:'věk známých,40,60,10', rok:'od-do',y:'vek',z:'typ-ucasti'}}
          func onclick(i:ezer) { var y:object
            clear();
            info.header(i); 
            js.highcharts_load();
            if (i) { last_par= i.par; last_menu= i; }
            info.header(last_menu); 
//            info.spinner(1);
            last_par.od= 2006; last_par.do= fdate('Y'); 
            last_par.prc= 0; 
            last_par.title= replace_fa(i.title,1);
            last_par.org= 0;
            y= php.chart_brno(last_par);
            if (y.err) alert(y.err)
            else { js.highcharts_show(y.chart); info.fill('',y.note); }
//            info.spinner(0);
          }
        }
        menu {title:'Dělená setkání - výpočty', type:'group', active:no, skill:'m'
          item {title:'[fa-exclamation] Přesun gnucast do xucast' par:{fce:'copy_gnucast',confirm:1} }
          item {title:'[fa-exclamation] Oprava překlepů podle *.csv'  par:{fce:'corr_jmeno',confirm:1} }
          item {title:'[fa-exclamation] ... a podle jmeno_corr.csv'   par:{fce:'jmeno_corr.csv',confirm:1} }
          item {title:'[fa-exclamation] Anonymizace jmen'         par:{fce:'id_jmeno',confirm:1} }
        }
        func onclick(i:ezer) { var graf:object
          clear(); js.highcharts_clear(); info.note= '';
          if (i.par.confirm 
            && !confirm(`Opravdu mám přepočítat údaje podle ${i.par.fce}?`)) return;
          info.header(i); 
          graf= php.stat_brno(i.par);
          info.fill('',graf.html);
        }
      }
    }
  }
  # ===================================================================================> system
  tabs sys {title:"[fa-cogs] Nastavení", _sys:'sys',  include:'onclick,ezer2.syst', active:*
    //panel oso {type:'right', title:'Osobní nastavení', _sys:'*', include:'onclick' }
    panel oso {type:'right', title:'[fa-user] Osobní nastavení', _sys:'*', include:'onclick,ezer2.pers' }
    # ===================================================================================== DATABÁZE
    panel db1 {type:'right', title:'[fa-database] Databáze', _sys:'*', include:'onload,ezer3.db', skill:'a'
      par:{
        infos: [
          {title:'[fa-object-group] Schema databáze',
           html: "tabulky RR a CAC jsou nezávislé. CACTHEME je provázána s CAC"}
        ],
        tables: {
          rr:       "id_rr>*,day_n,day,datum,subject",
          cac:      "id_cac>*,day_n,day,datum,subject",
          cactheme: "id_cactheme>*,url_theme,theme_eng,theme_cz",
          stamp:    "id_stamp>*,typ,kdy,pozn",
          cron:     "id_cron>*,batch,info,adr,replyto,par,subj,txt"
        },
        css: 'struc' // styl tabulek
    }}
    panel db2 {type:'right', title:'[fa-download] Backup', _sys:'str', skill:'m'
      menu m {type:'left', format:'f+'
        menu { title:'Backup databáze',type:'group',skill:'m', active:no
          item { title:"[fa-question] přehled záloh"                par:{typ:'download'}}
          item { title:"[fa-upload] zálohuj teď"                    par:{typ:'special'}}
          item { title:"[fa-download] obnova ze zálohy (jen local)" par:{typ:'restore'}}
          proc onclick(i) { 
            info.fill(conc(i.owner.title,' - ',i.title),' ');
            { eq(i.par.typ,'download'); 
              info.append("<i>zobrazené zálohy lze stáhnout kliknutím (nelze 'Stahnout jako ...')</i><br>");
              info.append(ask('sys_backup_make',i.par)) 
            | eq(i.par.typ,'special'); 
              [ confirm('Pokračovat?'); info.append(ask('sys_backup_make',i.par)) ]
            | eq(i.par.typ,'restore'); 
              // test na lokální běh na NTB 
              { eq(sys('ezer','server'),0); 
                [ confirm('Opravdu mám lokální databázi přepsat tou, kterou teď vybereš? 
                        Máš ji uschovanou? Po výběru se již rozběhne proces obnovy ...'); 
                  // načti obnovu
                  info.append(ask('sys_backup_make',i.par));
                ]
              | alert('obnovu ze zálohy nelze provádět na ostrém serveru');
              }
            }
          }
        }
      }
      use info: form right [12,4,,]     { tag:'i' }
    }
  }
  tabs doc  {title:"[fa-question] Nápověda", _sys:'*',  include:'onclick,ezer2.help' active:*, skill:'a' }
  tabs off  {type:'logoff', title:"[fa-power-off] Odhlásit"}
  # ------------------------------------------------------------------ tables
  table rr {
    number id_rr
    number day_n
    text day
    date datum { sql_pipe:'sql_date1'} 
    text state /* prepared|sent */
    text subject
    text title_cz
    text text_cz
    text title_en
    text text_en
    text from_en
    text errata
  }
  table cron { key_id:'id_cron'
    number id_cron
    text batch
    text info
    text adr
    text replyto
    text par
    text subj
    text txt
  }
}
// univerzální formulář pro levostranné menu
# ------------------------------------------------------------------ right
form right [,,*,50] { style:'width:calc(100% - 24px)'
  label head [0,0,*,50]  { title:'' }
  label msg  [0,35,*,20] { title:'' }
  label chart [10,50,800,400] { title:'', format:'n' }
  label note [0,55,*,] { title:'' }
  func Chart(on) {
    chart.display(on);
    if (on) chart= `<div id="container" style="width:800px; height:400px;"></div>`;
  }
  func header(i:ezer) { var i_owner:ezer
    clear(); i_owner= i.owner();
    msg= ''; note= '';
    head= replace_fa(`<div class='karta'>${i_owner.title} - ${i.title}</div>`);
  }
  func fill(x,y) {
    if (x) head= `<div class='karta'>${replace_fa(x)}</div>`;
    if (y) note= y
  }
  func append(y) {
    if (y) note= conc(note,y)
  }
}
