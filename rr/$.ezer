menu rr { type:'main', active:rr.richard
  # =================================================================================== RR
  tabs richard {title:"Myšlenky", _sys:'*', skill:'rr'
    panel rohr {type:'right', title:'Rozesílání'
      menu myslenka {type:'left'
        menu ukazat {title:'Zobrazení',type:'group'
          item dnesni {title:'dnešní myšlenky', par:{den:0,poslat:0} }
          item {title:'včerejší myšlenky', par:{den:-1,poslat:0} }
          item {title:'zítřejší myšlenky', par:{den:1,poslat:0} }
        }
        menu poslat {title:'Rozeslání',type:'group'
          item dnesni {title:'dnešní myšlenky', par:{den:0,poslat:1,opakovat:0} }
          item {title:' ... znovu', par:{den:0,poslat:1,opakovat:1} }
          item {title:' ... na martin@smidek.eu', 
                  par:{den:0,poslat:1,opakovat:0,test:'martin@smidek.eu'}, skill:'m'}
          item {title:'včerejší myšlenky', par:{den:-1,poslat:1,opakovat:0} }
          item {title:' ... znovu', par:{den:-1,poslat:1,opakovat:1} }
        }
        func onclick (i:ezer) {
          info.header(i);
          info.msg= '';
          info.fill('',php.rr_send(i.par));
        }
      }
      use info: form right [12,4,,]
    }
    panel plan {type:'right', title:'Opravy'
      use f: form _f [12,4,,] { format:'n' }
      use g: form _g [12,4,,] { format:'n' }
      menu nastav {type:'left', format:'f'
        menu obdobi {title:'Opravy',type:'group'
          item od {title:'Nastavení období'
            func onclick (i:ezer) { f.display(1); g.display(0); }
          }
          item {title:'Inicializace období'
            func onclick (i:ezer) { 
              f.display(1); g.display(0); 
              if(confirm("opravdu chceš smazat všechny nastavené datumy a vymazat stav?")) {
                php.query("UPDATE rr SET datum='0000-00-00',state='' ");
                f.b.browse_refresh(1);
              }
          }}
          item {title:'Oprava překladu'
            func onclick (i:ezer) { f.display(0); g.display(1); }
          }
        }
      }
      func onfirstfocus() {
        f.b.browse_load();
        g.b.browse_load()
      }
      # ---------------------------------------------------------------------------- opravy překladu
      form _g [,,*,100] {
        browse b [0,0,,] { rows:7, qry_rows:1, css_rows:'barva,1:yellow'
          show id_rr { data:rr.id_rr, format:'qs' }
          show [,,40,] { title:'den', data:rr.day, format:'qrs' }
          show [,,70,] { title:'datum', data:rr.datum, format:'qs' }
          show [,,270,] { title:'název', data:rr.subject, format:'qs' }
          show barva { expr:"IF(errata='',0,1)" }
          func onrowclick () { nacti() }
          func nacti() { g.load(id_rr) } }
        button [725,6,,] { title:'Dnes' 
          func onclick() {
            b.browse_seek(`datum='${now_sql()}'`);
            b.nacti()
        }}
        field czt [0,181,404,] { data:rr.title_cz }
        field ent [422,181,401,] { data:rr.title_en }
        button [778,6,,] { title:'Uložit' func onclick() { g.save(); g.load() } }
        edit cz [0,209,405,548] { type:'html', data:rr.text_cz, par:{toolbar:'Minimal'} }
        edit en [421,209,405,548] { type:'html', data:rr.text_en, par:{toolbar:'Minimal'} }
        label [423,19,,] { title:'Důvod pozměnění překladu' }
        edit errata [421,39,402,131] { data:rr.errata }
      }
      # --------------------------------------------------------------------------- nastavení období
      form _f [,,*,500] {
        browse b [0,0,,] { rows:24, qry_rows:1
          show id_rr { data:rr.id_rr, format:'qs' }
          show den [,,30,] { title:'pořadí', data:rr.day_n, format:'rqs' }
          show [,,40,] { title:'den', data:rr.day, format:'rqs' }
          show [,,20,] { title:'ps', data:rr.state, format:'qs' }
          show datum [,,70,] { title:'datum', data:rr.datum, format:'rqs' }
          show [,,140,] { title:'název', data:rr.subject, format:'tqs' }
          func onsubmit() {
            form.den= den; form.datum= datum; } 
        }
        label [340,10,,] { title:"Nastav pořadové číslo dne, počáteční datum a počet dnů" }
        field den [340,40,30,] { format:'t'}
        field datum [380,40,90,] { type:'date', format:'rt' }
        field pocet [480,40,40,] { format:'t', value:'1' }
        button [530,40,,] { title:'nastav'
          func onclick() { var ret:object
            ret= php.rr_nastav(den,datum,pocet);
            msg= ret.msg;
            datum= replace(sql2date(ret.next),' ','');
            den= den + pocet;
            f.b.browse_seek(`day_n=${den-1}`);
        }}
        button [582,40,,] { title:'zruš'
          func onclick() { 
            msg= php.rr_zrus(den,pocet);
            f.b.browse_refresh();
        }}
        button [366,65,,] { title:'dnes^'
          func onclick() { datum= now(); datum.change(); }
        }
        button [418,65,,] { title:'^poslední'
          func onclick() { 
            datum= replace(sql2date(php.select('MAX(datum + INTERVAL 1 DAY)','rr')),' ',''); 
            datum.change(); 
        }}
        label msg [340,92,300,200]
      }
    }
  }
  # ===================================================================================> CAC
  tabs uvahy {title:"CAC", _sys:'*', skill:'cac', include:'onload'}
  # ===================================================================================> CAC
  tabs maily {title:"Maily", _sys:'*', skill:'mail'
    panel maily { type:'right', title:'Upomínací maily'
      use info: form right [12,4,,]
      menu m {type:'left', format:'f'
        menu p {title:'Editace a plán',type:'group'
          item {title:'[fa-cutlery] Text'
            func onclick(i:ezer) { 
              info.header(i); clear();
              info.fill('',php.note_text('ja'));
            }
          }
          item k {title:'[fa-calendar-check-o] Plán'
            func onclick(i:ezer) { 
              info.header(i); clear();
              info.append("rozesílá se 3. den v měsíci, je to třeba v Plánovači úloh na Synology");
            }
          }
        }
        menu poslat {title:'Rozeslání', type:'group', active:no
          item dnesni {title:'upomínky nyní všem', par:{adresy:'*'}}
          item {title:' ... na Miloše', par:{adresy:'mv'}, skill:'m'}
          item {title:' ... na martin@smidek.eu', par:{adresy:'ja'}, skill:'m'}
          func onclick(i:ezer) { 
            info.header(i); clear();
            info.fill('',php.note_send(i.par.adresy));
          }
        }
        menu {title:'Log file',type:'group', skill:'a'
          item {title:'[fa-question] odeslání mailu Pokladna',  par:{typ:'mail',subj:'pokladna'} }
          func onclick (i:ezer) { var cac:object
            clear(); info.header(i); panel.display(0,'.'); 
            info.fill('',php.stamp_show(i.par.typ,i.par.subj));
          }
        }
      }
    }
  }
  # ===================================================================================> system
  tabs sys {title:"[fa-cogs] Nastavení", _sys:'sys',  include:'onclick,ezer2.syst', active:*
    //panel oso {type:'right', title:'Osobní nastavení', _sys:'*', include:'onclick' }
    panel oso {type:'right', title:'[fa-user] Osobní nastavení', _sys:'*', include:'onclick,ezer2.pers' }
    # ===================================================================================== DATABÁZE
    panel db1 {type:'right', title:'[fa-database] Databáze', _sys:'*', include:'onload,ezer3.db', skill:'a'
      par:{
        infos: [
          {title:'[fa-object-group] Schema databáze',
           html: "tabulky RR a CAC jsou nezávislé"}
        ],
        tables: {
          rr:   "id_rr>*,day_n,day,datum,subject",
          cac:  "id_cac>*,day_n,day,datum,subject"
        },
        css: 'struc' // styl tabulek
    }}
    panel db2 {type:'right', title:'[fa-download] Backup', _sys:'str', skill:'m'
      menu m {type:'left', format:'f+'
        menu { title:'Backup databáze',type:'group',skill:'m', active:no
          item { title:"[fa-question] přehled záloh"                par:{typ:'download'}}
          item { title:"[fa-upload] zálohuj teď"                    par:{typ:'special'}}
          item { title:"[fa-download] obnova ze zálohy (jen local)" par:{typ:'restore'}}
          proc onclick(i) { 
            info.fill(conc(i.owner.title,' - ',i.title),' ');
            { eq(i.par.typ,'download'); 
              info.append("<i>zobrazené zálohy lze stáhnout kliknutím (nelze 'Stahnout jako ...')</i><br>");
              info.append(ask('sys_backup_make',i.par)) 
            | eq(i.par.typ,'special'); 
              [ confirm('Pokračovat?'); info.append(ask('sys_backup_make',i.par)) ]
            | eq(i.par.typ,'restore'); 
              // test na lokální běh na NTB 
              { eq(sys('ezer','server'),0); 
                [ confirm('Opravdu mám lokální databázi přepsat tou, kterou teď vybereš? 
                        Máš ji uschovanou? Po výběru se již rozběhne proces obnovy ...'); 
                  // načti obnovu
                  info.append(ask('sys_backup_make',i.par));
                ]
              | alert('obnovu ze zálohy nelze provádět na ostrém serveru');
              }
            }
          }
        }
      }
      use info: form right [12,4,,]     { tag:'i' }
    }
  }
  tabs doc  {title:"Nápověda", _sys:'*',  include:'onclick,ezer2.help' active:*, skill:'a' }
  tabs off  {type:'logoff', title:"[fa-power-off] Odhlásit"}
  # ------------------------------------------------------------------ tables
  table rr {
    number id_rr
    number day_n
    text day
    date datum { sql_pipe:'sql_date1'} 
    text state /* prepared|sent */
    text subject
    text title_cz
    text text_cz
    text title_en
    text text_en
    text from_en
    text errata
  }
}
// univerzální formulář pro levostranné menu
# ------------------------------------------------------------------ right
form right [,,*,50] { style:'width:calc(100% - 24px)'
  label head [0,0,*,50]  { title:'' }
  label msg  [0,35,*,20] { title:'' }
  label note [0,55,*,] { title:'' }
  func header(i:ezer) { var i_owner:ezer
    clear(); i_owner= i.owner();
    msg= ''; note= '';
    head= replace_fa(`<div class='karta'>${i_owner.title} - ${i.title}</div>`);
  }
  func fill(x,y) {
    if (x) head= `<div class='karta'>${replace_fa(x)}</div>`;
    if (y) note= y
  }
  func append(y) {
    if (y) note= conc(note,y)
  }
}
