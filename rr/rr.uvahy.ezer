panel testy { type:'right', title:'Úvahy a meditace'
  use info: form right [12,4,,]
  use g: form _g [12,38,,] { tag:'g', format:'n' }
  use h: form _h [12,38,,] { tag:'h', format:'n' }
  use b: form _b [12,38,,] { tag:'b', format:'n' }
  menu m {type:'left', format:'f' 
    menu p {title:'Překládání úvah CAC',type:'group'
      item k {title:'[fa-calendar-check-o] Plánovací kalendář'
        func onclick(i:ezer) { 
          info.header(i); panel.display(2,'g'); clear();
          g.Init();
        }
      }
      item {title:'[fa-exclamation] Přidat úvahu z daného data', active:no
        func onclick(i:ezer) { 
          info.header(i); panel.display(2,'g'); clear();
          g.Init();
          Choose.Show();
        }
      }
      item {title:'[fa-cutlery] Přehled témat'
        func onclick(i:ezer) { 
          info.header(i); panel.display(2,'h'); clear();
          g.b.browse_refresh();
        }
      }
      item {title:'[fa-book] Názvy biblických knih'
        func onclick(i:ezer) { 
          info.header(i); panel.display(2,'b'); clear();
          b.b.browse_refresh();
        }
      }
    }
    menu {title:'Log file',type:'group', skill:'a'
      item {title:'[fa-question] pokus o stažení Meditace', par:{typ:'cac'} }
      item {title:'[fa-question] odeslání Myšlenek',        par:{typ:'rr'} }
      item {title:'[fa-question] odeslání mailu Pokladna',  par:{typ:'mail',subj:'pokladna'} }
      func onclick (i:ezer) { var cac:object
        clear(); info.header(i); panel.display(0,'.'); 
        info.fill('',php.stamp_show(i.par.typ,i.par.subj));
      }
    }
  }
  # ==> incializace
  func onfirstfocus() {
    g.b.browse_load(); g.LoadCac(); g.GoNewCac(); h.LoadTema(); b.LoadBible();
  }
  func onfocus() { info.Chart(0) }
  # --------------------------------------------------------------------------==> biblické knihy
  form _b [,,*,100] {
    view k: table bible_kniha
    func LoadBible() { b.browse_load(`k.bible=1 AND k.poradi<999`) }
    browse b [0,0,,] { rows:20, qry_rows:1, group_by:'kniha'
      show { data:k.id_bible_kniha }
      show nazev [,,150,] { title:'kniha', expr:"MAX(k.nazev)", format:'tq*s'  }
      show kniha [,,50,] { title:'zkratka', data:k.kniha, format:'q*s' }
      show aliasy [,,171,] { title:'aliasy', expr:"GROUP_CONCAT(k.alias)", format:'tq*s' }
      show poradi [,,0,] { title:'No.', expr:"MAX(k.poradi)", format:'rq*s+' }
      func onrowclick() { var i:number, v:text
        copy_by_name(b,form);
        v= `<h3>${nazev} - začátek první kapitoly</h3>`;
        for (i=1; i<=5; i++) {
          v= conc(`${v} <sup>${i}</sup>`,
              php.select('text','bible',`kniha='${kniha}' AND kapitola=1 AND vers=${i}`,'setkani'));
        }
        vv= v;
      }
      func onclick() { 
        if (poradi.width()) { poradi.width(0); aliasy.width(171); }
        else { poradi.width(30); aliasy.width(140); }
        form.poradi.display(poradi.width()?1:0);
      }
    }    
    field kniha [412,33,30,] { title:'^pro knihu', format:'d' }
    field nazev [460,33,181,] { format:'' }
    field poradi [660,33,30,] { title:'^No. ', format:'nrf' }
    field aliasy [412,73,230,] { title:'^rozeznáváme jako aliasy ' }
    label [412,104,300,30] { title:"tento čárkami oddělený seznam alternativních názvů 
        <br>lze po úvaze změnit a poté uložit (také název)" 
    }
    // ==> . tlačítka
    label [412,142,130,34] { css:'parm' }
    button [421,150,,] { title:'[fa-save] Uložit'
      func onclick() { 
        php.bib_save_aliases(kniha,aliasy,nazev.changed()?nazev:'',poradi.changed()?poradi:0); 
        b.browse_row(); b.onrowclick() 
    }}
    button [485,150,,] { title:'[fa-undo] Zpět'
      func onclick() { b.onrowclick() }}
    // ==> . ukázka textu
    label vv [412,180,420,194] 
  }
  # --------------------------------------------------------------------------==> přehled témat
  form _h [,,*,100] {
    view t: table cactheme
    view c: table cac { join_type:'LEFT', join:"USING (id_cactheme)" }
    func LoadTema() { b.browse_load() }
    browse b [0,0,,] { rows:20, qry_rows:1, group_by:'theme_eng'
      show [,,250,] { title:'originál', data:t.theme_eng, format:'qs' }
      show [,,250,] { title:'překlad', data:t.theme_cz, format:'qs' }
      show od [,,90,] { title:'od', expr:"MIN(c.datum)", sql_pipe:'sql_date', format:'rqs-' }
      show do [,,90,] { title:'do', expr:"MAX(c.datum)", sql_pipe:'sql_date', format:'rqs' }
    }    
  }
  # --------------------------------------------------------------------------==> originál a překlad
  form _g [,,*,100] {
    const top=110
    var edited: number,  // cid editovaného záznamu nebo 0
        curr_row: number
    # ==> globální funkce
    func Init() { b.browse_refresh(); Free() }
    func LoadCac() {
      g.load(b.id_cac); t.load(b.id_cactheme); 
      text_eng= b.text_eng;
      ref_tema= `<a href="${b.url_theme}" target="cac"><i class='fa fa-share'></i> CAC</a>`; 
      ref_text= `<a href="${b.url_text}" target="cac"><i class='fa fa-share'></i> CAC</a>`; 
    } 
    func GoNewCac() { var idc:number
      idc= php.select('id_cac','cac',"title_eng!='' ORDER BY datum DESC LIMIT 1");
      b.browse_seek(`id_cac=${idc}`);
    }
    # ==> . lokální funkce
    func Open(cid) { var kdo: text
      // zkontrolujme, zda je v pořádku stav
      if (!b.barva) {
        php.cac_change_state(b.id_cac,2); b.browse_row();
      }
      elseif (b.preklada!=sys('user','id_user')) {
        kdo= conc(sys('user','forename'),' ',sys('user','surname'));
        if (confirm(`k tomuto překladu se zná ${kdo} opravdu si ho chceš přepsat na sebe?`)) {
          php.cac_change_state(b.id_cac,2); b.browse_row();
        }
        else {
          zpet()
        }
      }
      edited= cid; form_state('c',''); 
    }
    func Free() { edited= 0; form_state('','c'); }
    func zmena_stavu(s) {
      if (form.same()) { 
        php.cac_change_state(b.id_cac,s); b.browse_row();
      }
      else warning(`nejprve proveď [Uložení změn] nebo [Vrácení změn]`)
    }
    func uloz() {
      // případná změna téma
      if (theme_cz.changed()) { t.save(); t.load(); b.browse_seek() }
      // český překlad uložíme zvlášť
      if (text_cz.changed()) { php.cac_save_fld(form.key(),'text_cz',text_cz); text_cz.plain() }
      // zbylé už normálně
      if (!g.same()) { g.save(); g.load()  }
      cac_lock('off');
    }
    func zpet() { g.load(); cac_lock('off'); }
    func form_state(on,off) {
      form.enable(1,on); form.enable(0,off);    
    }
    # ==> .. ochrana editace více uživateli
    func onchanged () { 
      if (!edited) { 
        cac_lock('on'); echo('changed'); 
        changed_cz= fdate('Y-m-d H:i:s'); changed_cz.change();  
    }}
    func cac_lock(on) { var lock:object 
      if (form.key()) {
        lock= php.table_lock(on,'cac',form.key()); 
        if (on=='on') {
          if (!lock.ok) { alert(`${lock.info}`); zpet(); }
          else if (!edited) { Open(form.key()); }
        }
        else { Free(); } 
      }
    }
    // ==> seznam meditací
    view c: table cac
    view t: table cactheme { join_type:'LEFT', join:"USING (id_cactheme)" }
    view u: table _user { join_type:'LEFT', join:"ON u.id_user=c.preklada" }
    // ==> změny stavu seznamu
    button [0,0,,] { title:'[fa-plus]' 
      func onclick() { 
        alert(php.cac_read_medits('USER')); 
        g.b.browse_refresh();
    }}
    button [24,0,,] { title:'[fa-calendar-check-o] Dnes' func onclick() { GoNewCac() }}
    label [87,2,38,16] { title:" <span class='silver pointer'>volné</span>", 
        help:"necháme to na DeepL"
      func onclick() { zmena_stavu(0) }}
    label [121,2,67,16] { title:" <span class='blue pointer'>rezervováno</span>"  
        help:"zavazuji se k úpravě překladu, chci dostat mail až bude anglický text dostupný"
      func onclick() { zmena_stavu(1) }}
    label [195,2,63,16] { title:" <span class='yellow pointer'>překládáno</span>" 
        help:"teď na tom pracuji a NECHCI aby to bylo zobrazeno na webu"
      func onclick() { zmena_stavu(2) }}
    label [263,2,55,16] { title:" <span class='green pointer'>upraveno</span>"    
        help:"upravil jsem formát ale angličtinou si nejsem zcela jist"
      func onclick() { zmena_stavu(3) }}
    label [322,2,59,16] { title:" <span class='green2 pointer'>přeloženo</span>"  
        help:"upravil jsem formát i angličtinu"
      func onclick() { zmena_stavu(4) }}
    button [387,0,,] { title:'[fa-question]', help:'ukáže odlišnost autorského překladu od DeepL' 
      func onclick() { Diff.Show(b.id_cac) }}
    // 
    browse b [0,24,,] { rows:7, qry_rows:1, css_rows:'barva,1:blue,2:yellow,3:green,4:green2'
      show id_cac { data:c.id_cac }
      show id_cactheme { data:t.id_cactheme }
      show { data:u.id_user }
      show url_text { data:c.url_text }
      show url_theme { data:t.url_theme }
      show text_eng { data:c.text_eng }
      show datum [,,90,] { title:'datum', data:c.datum, format:'rqs+' }
      show tema [,,60,] { title:'téma', data:t.theme_eng, format:'tq' }
      show [,,185,] { title:'název', data:c.title_eng, format:'tq' }
      show preklad [,,40,] { title:'překládá', data:u.username, format:'q' }
      show import [,,0,] { title:'načteno z CAC', data:c.imported_eng, format:'q' }
      show barva { data:c.stav }
      show preklada { data:c.preklada }
      func onrowclick () { 
        if (!edited) { LoadCac(); curr_row= b.browse_active(); }
        else { b.browse_active(curr_row); alert('ulož nabo zahoď změny'); }
      }
      func onclick() { 
        if (import.width()) { import.width(0); tema.width(60); preklad.width(40) }
        else { import.width(101); tema.width(0); preklad.width(0) }
      }
      menu { type:'context'
        item { title:'tento text chci překládat'    func onclick() { zmena_stavu(1) } }
        item { title:'... pracuji na překladu'      func onclick() { zmena_stavu(2) } }
        item { title:'hotovo: upraveno po DeepL'    func onclick() { zmena_stavu(3) } }
        item { title:'hotovo: přeloženo'            func onclick() { zmena_stavu(4) } }
        item { title:'-odvolávám a uvolňuji jinému' func onclick() { zmena_stavu(0) } }
        item { title:'porovnání s původním DeepL'   func onclick() { Diff.Show(id_cac) } }
        item { title:'=vymazat originál i překlad' skill:'m' func onclick() { var msg:text
            msg= php.cac_make_free(id_cac); 
            if (msg) alert(msg);
            b.browse_row(); LoadCac()
        } }
        item { title:'znovu načíst den bez překladu' skill:'m' func onclick() { 
            clear(); php.cac_read_medit('AGAIN',date2sql(datum)); b.browse_row(); LoadCac() 
        } }
        item { title:'přeložit den pomocí DeepL'     skill:'m' func onclick() { 
            clear(); 
            if (!text_cz || confirm("opravdu přepsat přeložený text?")) { 
              php.cac_through_DeepL(id_cac); b.browse_row(); LoadCac() 
        } } }
      }
    }
    // ==> popis 
    label [421,0,272,40] { css:'work', style:'padding:5px', title:
        "<b>Stručný popis:</b> pod seznamem můžeš upravit překlad, pak ulož změnu a uprav stav 
          kliknutím na barvu nad seznamem" 
        }
    label [674,34,29,16] { title:
      "<a href='https://notability.com/n/VXEqiurh5_dKdZ5fcna5a' target='pdf' 
        style='background:#365faf;color:#ffffff;cursor:pointer'>&nbsp;více&nbsp;</a>", 
    }
    label [710,0,114,57] { css:'parm' }
    button [718,8,,] { tag:'c', title:'[fa-save] Uložení změn', func onclick() { uloz() }}
    button [719,31,,] { tag:'c', title:'[fa-undo] Vrácení změn', func onclick() { 
      if (confirm("Opravdu mám zahodit poslední úpravy?")) zpet();
    }}
    field id_cac { data:c.id_cac }
    field id_cactheme { data:t.id_cactheme }
    // ==> pole pro revize překladu a pro literaturu
    edit [0,220,402,60] { data:c.errata, title:'^Důvod revize překladu (s podpisem prosím)' }
    edit [421,60,402,190] { type:'html', data:c.reference, title:'^Reference', par:{toolbar:'Cac'} }
    // ==> editační pole CZ
    field theme_cz [0,top+181,399,] { data:t.theme_cz }
    field [0,top+208,399,] { data:c.title_cz }
    edit text_cz [0,top+237,405,548] { type:'html', data:c.text_cz, par:{toolbar:'Cac'} }
    field changed_cz { data:c.changed_cz }
    // ==> editační pole ENG
    field [422,top+154,344,] { data:c.author }
    field [422,top+181,344,] { data:t.theme_eng }
    field [422,top+208,344,] { data:c.title_eng }
    label ref_tema [780,top+184,46,16] { style:'cursor:pointer' }
    label ref_text [780,top+211,46,16] { style:'cursor:pointer' }
//    edit text_eng [421,top+236,405,548] { type:'html', data:c.text_eng, par:{toolbar:'Cac'} }
    label text_eng [421,top+238,390,543] { style:'overflow-x:auto;border:1px solid #d3d3d3;padding:5px;font-size:10pt' }
  }
  // ==> výběr meditace k dodatečnnému překladu
  panel Choose [0,0,400,200] { type:'popup', title:"Dodatečné nahrávání úvah CAC"
    var uvaha:object,
        ymd=''
    func Show() { var dne:text, x:object
      uvaha= {};
      if (!ymd) d.den= now();
      d.nazev= '';
      d.tema= '';
      d.enable(0,'u|p');
      panel.popup();
    }
    use d: form {
      field den [82,10,87,] { type:'date', title:'Datum úvahy', format:'tr' } 
      label [10,39,272,20] { title:'název' }
      label nazev [10,59,272,50]
      label [10,118,272,20] { title:'téma' }
      label tema  [10,138,272,50]
      button [300,10,,] { title:'[fa-question] Najdi úvahu' 
        func onclick() { 
          ymd= date2sql(den);
          form.enable(0,'u|p');
          uvaha= php.cac_save_medit('TEST',ymd,0);
          form.enable(1,'u');
          //echo(debug(uvaha));
          d.nazev= uvaha.title;
          d.tema= uvaha.tema;
      }}
      button [300,40,,] { tag:'u', title:'[fa-save] Ulož' 
        func onclick() { 
          uvaha= php.cac_save_medit('TEST',ymd,1);
          form.enable(1,'p');
          g.b.browse_seek(`c.datum="${ymd}"`);
      }}
      button [300,70,,] { tag:'p', title:'[fa-magic] Přelož' 
        func onclick() { var e:object
          e= php.cac_through_DeepL(uvaha.idc);
          nazev= `${nazev}<hr>${e.title_cz}`;
          tema= `${tema}<hr>${e.theme_cz}`;
          g.b.browse_seek(`c.datum="${ymd}"`);
      }}
      button [300,100,,] { title:'[fa-undo] Zpět' 
        func onclick() { panel.hide(0);
      }}
    }
  }
  // ==> zobrazení rozdílu proti původnímu DeepL
  panel Diff [0,0,400,600] { type:'popup' 
    var idc: number
    func Show(_idc) { 
      clear(); idc= _idc; 
      compare();
      panel.popup();
    }
    func compare() { var html:text
      html= php.cac_show_diff(idc,{html:d.html,ins:eq(d.typ,1,3),del:eq(d.typ,2,3)});
      d.x= html;
    }
    use d: form {
      label [7,4,71,17] { title:'ukázat, co je' }
      radio typ [75,0,212,17] { format:'t', value:'3'
        case [0,0,80,] { title:"přidané", value:'1' }
        case [70,0,80,] { title:"zrušené", value:'2' }
        case [140,0,80,] { title:"oboje", value:'3' }
        func onchange() {compare()} 
      }
      check html [282,0,112,17] { title:"potlačit html tagy", value:'1', format:'t' 
        func onchange() {compare()} }
      label x [0,30,400,570] { style:'overflow-x:overlay;background:#eee' }
    }
  }
  // ==> tabulky CAC
  table bible_kniha { key_id:'id_bible_kniha', db:'setkani'
    number id_bible_kniha
    text kniha
    text nazev
    text alias
  }
  table bible { key_id:'id_bible', db:'setkani'
    number id_bible
    text kniha
    text kapitola
    text vers
    text text
  }
  table cac { key_id:'id_cac'
    number id_cac
    number id_cactheme
    date datum { sql_pipe:'sql_date'} 
    text stav // volné, rezervace, upravené, přeložené
    text errata
    text url_theme
    text url_text
    text author
    text reference
    text preklada
    text theme_eng
    text title_eng
    text text_eng
    date imported_eng
    text theme_cz
    text title_cz
    text text_cz
    text text_cz_deepl
    date changed_cz
  }
  table cactheme { key_id:'id_cactheme'
    number id_cactheme
    text url_theme
    text theme_eng
    text theme_cz
  }
  table _user {
    number id_user
    text username
  }
}
