panel testy {type:'right', title:'Úvahy a meditace'
  use info: form right [12,4,,]
  use g: form _g [12,38,,] { tag:'g', format:'n' }
  use h: form _h [12,38,,] { tag:'h', format:'n' }
  use b: form _b [12,38,,] { tag:'b', format:'n' }
  menu m {type:'left', format:'f'
    menu p {title:'Překládání úvah CAC',type:'group'
      item k {title:'[fa-calendar-check-o] Plánovací kalendář'
        func onclick(i:ezer) { 
          info.header(i); panel.display(2,'g'); clear();
          g.b.browse_refresh();
        }
      }
      item {title:'[fa-cutlery] Přehled témat'
        func onclick(i:ezer) { 
          info.header(i); panel.display(2,'h'); clear();
          g.b.browse_refresh();
        }
      }
      item {title:'[fa-book] Názvy biblických knih'
        func onclick(i:ezer) { 
          info.header(i); panel.display(2,'b'); clear();
          b.b.browse_refresh();
        }
      }
    }
    menu {title:'Log file',type:'group', skill:'a'
      item {title:'[fa-question] pokus o stažení Meditace', par:{typ:'cac'} }
      item {title:'[fa-question] odeslání Myšlenek',        par:{typ:'rr'} }
      item {title:'[fa-question] odeslání mailu Pokladna',  par:{typ:'mail',subj:'pokladna'} }
      func onclick (i:ezer) { var cac:object
        clear(); info.header(i); g.display(0); h.display(0); 
        info.fill('',php.stamp_show(i.par.typ,i.par.subj));
      }
    }
  }
  // ==> incializace
  func onfirstfocus() {
    g.b.browse_load(); g.b.nacti(); g.b.nejnovejsi(); h.b.nacti(); b.b.nacti();
//    m.p.k.click(); 
  }
  # --------------------------------------------------------------------------==> biblické knihy
  form _b [,,*,100] {
    view k: table bible_kniha
    browse b [0,0,,] { rows:20, qry_rows:1, group_by:'kniha'
      show { data:k.id_bible_kniha }
      show nazev [,,150,] { title:'kniha', expr:"MAX(k.nazev)", format:'tq*s'  }
      show kniha [,,50,] { title:'zkratka', data:k.kniha, format:'q*s' }
      show aliasy [,,171,] { title:'aliasy', expr:"GROUP_CONCAT(k.alias)", format:'tq*s' }
      show poradi [,,0,] { title:'No.', expr:"MAX(k.poradi)", format:'rq*s+' }
      func nacti() { b.browse_load(`k.bible=1 AND k.poradi<999`) }
      func onrowclick() { var i:number, v:text
        copy_by_name(b,form);
        v= `<h3>${nazev} - začátek první kapitoly</h3>`;
        for (i=1; i<=5; i++) {
          v= conc(`${v} <sup>${i}</sup>`,
              php.select('text','bible',`kniha='${kniha}' AND kapitola=1 AND vers=${i}`,'setkani'));
        }
        vv= v;
      }
      func onclick() { 
        if (poradi.width()) { poradi.width(0); aliasy.width(171); }
        else { poradi.width(30); aliasy.width(140); }
        form.poradi.display(poradi.width()?1:0);
      }
    }    
    field kniha [412,33,30,] { title:'^pro knihu', format:'d' }
    field nazev [460,33,181,] { format:'' }
    field poradi [660,33,30,] { title:'^No. ', format:'nrf' }
    field aliasy [412,73,230,] { title:'^rozeznáváme jako aliasy ' }
    label [412,104,300,30] { title:"tento čárkami oddělený seznam alternativních názvů 
        <br>lze po úvaze změnit a poté uložit (také název)" 
    }
    // ==> . tlačítka
    label [412,142,130,34] { css:'parm' }
    button [421,150,,] { title:'[fa-save] Uložit'
      func onclick() { 
        php.bib_save_aliases(kniha,aliasy,nazev.changed()?nazev:'',poradi.changed()?poradi:0); 
        b.browse_row(); b.onrowclick() 
    }}
    button [485,150,,] { title:'[fa-undo] Zpět'
      func onclick() { b.onrowclick() }}
    // ==> . ukázka textu
    label vv [412,180,420,194] 
  }
  # --------------------------------------------------------------------------==> přehled témat
  form _h [,,*,100] {
    view t: table cactheme
    view c: table cac { join_type:'LEFT', join:"USING (id_cactheme)" }
    browse b [0,0,,] { rows:20, qry_rows:1, group_by:'theme_eng'
      show [,,250,] { title:'originál', data:t.theme_eng, format:'qs' }
      show [,,250,] { title:'překlad', data:t.theme_cz, format:'qs' }
      show od [,,90,] { title:'od', expr:"MIN(c.datum)", sql_pipe:'sql_date', format:'rqs-' }
      show do [,,90,] { title:'do', expr:"MAX(c.datum)", sql_pipe:'sql_date', format:'rqs' }
      func nacti() { b.browse_load() }
    }    
  }
  # --------------------------------------------------------------------------==> originál a překlad
  form _g [,,*,100] {
    const top=110
    view c: table cac
    view t: table cactheme { join_type:'LEFT', join:"USING (id_cactheme)" }
    view u: table _user { join_type:'LEFT', join:"ON u.id_user=c.preklada" }
    label [90,2,42,16] { title:" <span class='silver pointer'>volné</span>"       
      func onclick() { zmena_stavu(0) }}
    label [127,2,67,16] { title:" <span class='blue pointer'>rezervováno</span>"  
      func onclick() { zmena_stavu(1) }}
    label [204,2,66,16] { title:" <span class='yellow pointer'>překládáno</span>" 
      func onclick() { zmena_stavu(2) }}
    label [281,2,55,16] { title:" <span class='green pointer'>upraveno</span>"    
      func onclick() { zmena_stavu(3) }}
    label [348,2,59,16] { title:" <span class='green2 pointer'>přeloženo</span>"  
      func onclick() { zmena_stavu(4) }}
    browse b [0,24,,] { rows:7, qry_rows:1, css_rows:'barva,1:blue,2:yellow,3:green,4:green2'
      show id_cac { data:c.id_cac }
      show id_cactheme { data:t.id_cactheme }
      show { data:u.id_user }
      show url_text { data:c.url_text }
      show url_theme { data:t.url_theme }
      show datum [,,90,] { title:'datum', data:c.datum, format:'rqs+' }
      show tema [,,60,] { title:'téma', data:t.theme_eng, format:'tq' }
      show [,,185,] { title:'název', data:c.title_eng, format:'tq' }
      show preklad [,,40,] { title:'překládá', data:u.username, format:'q' }
      show import [,,0,] { title:'načteno z CAC', data:c.imported_eng, format:'q' }
      show barva { data:c.stav }
      func onrowclick () { nacti() }
      func onclick() { 
        if (import.width()) { 
          import.width(0); tema.width(60); preklad.width(40) }
        else {
          import.width(101); tema.width(0); preklad.width(0) }
      }
      func nacti() {
        g.load(id_cac); t.load(id_cactheme); 
        ref_tema= `<a href="${url_theme}" target="cac"><i class='fa fa-share'></i> CAC</a>`; 
        ref_text= `<a href="${url_text}" target="cac"><i class='fa fa-share'></i> CAC</a>`; 
      } 
      func nejnovejsi() { var idc:number
        idc= php.select('id_cac','cac',"title_eng!='' ORDER BY datum DESC LIMIT 1");
        b.browse_seek(`id_cac=${idc}`);
      }
      menu { type:'context'
        item { title:'tento text chci překládat'    func onclick() { zmena_stavu(1) } }
        item { title:'... pracuji na překladu'      func onclick() { zmena_stavu(2) } }
        item { title:'hotovo: upraveno po DeepL'    func onclick() { zmena_stavu(3) } }
        item { title:'hotovo: přeloženo'            func onclick() { zmena_stavu(4) } }
        item { title:'-odvolávám a uvolňuji jinému' func onclick() { zmena_stavu(0) } }
        item { title:'=vymazat originál i překlad' skill:'m' func onclick() { var msg:text
            msg= php.cac_make_free(id_cac); 
            if (msg) alert(msg);
            b.browse_row(); nacti()
        } }
        item { title:'znovu načíst den bez překladu' skill:'m' func onclick() { 
            clear(); php.cac_save_medit_from('AGAIN',date2sql(datum)); b.browse_row(); nacti() 
        } }
        item { title:'přeložit den pomocí DeepL'     skill:'m' func onclick() { 
            clear(); 
            if (!text_cz || confirm("opravdu přepsat přeložený text?")) { 
              php.cac_through_DeepL(id_cac); b.browse_row(); nacti() 
        } } }
      }
    }
    // ==> popis
    label [421,0,272,40] { css:'work', style:'padding:5px', title:
        "<b>Stručný popis:</b> pod seznamem můžeš upravit překlad, pak ulož změnu a uprav stav 
          kliknutím na barvu nad seznamem" 
        }
    label [674,34,29,16] { title:
      "<a href='https://notability.com/n/VXEqiurh5_dKdZ5fcna5a' target='pdf' 
        style='background:#365faf;color:#ffffff;cursor:pointer'>&nbsp;více&nbsp;</a>", 
    }
    // #==> funkce
    func zmena_stavu(s) {
      if (form.same()) { 
        php.cac_change_state(b.id_cac,s); b.browse_row();
      }
      else warning(`nejprve proveď [Uložení změn] nebo [Vrácení změn]`)
    }
    button [0,0,,] { title:'[fa-plus]' 
      func onclick() { 
        alert(php.cac_read_medits('USER')); 
        g.b.browse_refresh();
    }}
    button [24,0,,] { title:'[fa-calendar-check-o] Dnes' func onclick() { b.nejnovejsi() }}
    label [710,0,114,57] { css:'parm' }
    button [718,8,,] { title:'[fa-save] Uložení změn', func onclick() { 
      // případná změna téma
      if (theme_cz.changed()) {
        t.save(); t.load(); b.browse_seek()
      }
      if (!g.same()) {
        changed_cz= fdate('Y-m-d H:i:s'); changed_cz.change();
        g.save(); g.load() 
      }
    } }
    button [719,31,,] { title:'[fa-undo] Vrácení změn', func onclick() { g.load() } }
    field id_cac { data:c.id_cac }
    field id_cactheme { data:t.id_cactheme }
    // ==> pole pro revize překladu a pro literaturu
    edit [0,220,402,60] { data:c.errata, title:'^Důvod revize překladu (s podpisem prosím)' }
    edit [421,60,402,190] { type:'html', data:c.reference, title:'^Reference', par:{toolbar:'Cac'} }
    // ==> editační pole CZ
    field theme_cz [0,top+181,399,] { data:t.theme_cz }
    field [0,top+208,399,] { data:c.title_cz }
    edit text_cz [0,top+237,405,548] { type:'html', data:c.text_cz, par:{toolbar:'Cac'} }
    field changed_cz { data:c.changed_cz }
    // ==> editační pole ENG
    field [422,top+154,344,] { data:c.author }
    field [422,top+181,344,] { data:t.theme_eng }
    field [422,top+208,344,] { data:c.title_eng }
    label ref_tema [780,top+184,46,16] { style:'cursor:pointer' }
    label ref_text [780,top+211,46,16] { style:'cursor:pointer' }
    edit text_eng [421,top+236,405,548] { type:'html', data:c.text_eng, par:{toolbar:'Cac'} }
  }
  // ==> podrobný návod k použití 
  panel vysvetli [0,0,400,200] { type:'popup'
    use r: form {
      label { title:"tady bude podrobný způsob použití, například bude vysvětleno, že na řádek
          seznamu úvah lze kliknout pravým tlačítkem myši a změnit stav ..." }      
    }
  }
  // ==> tabulky CAC
  table bible_kniha { key_id:'id_bible_kniha', db:'setkani'
    number id_bible_kniha
    text kniha
    text nazev
    text alias
  }
  table bible { key_id:'id_bible', db:'setkani'
    number id_bible
    text kniha
    text kapitola
    text vers
    text text
  }
  table cac { key_id:'id_cac'
    number id_cac
    number id_cactheme
    date datum { sql_pipe:'sql_date'} 
    text stav // volné, rezervace, upravené, přeložené
    text errata
    text url_theme
    text url_text
    text author
    text reference
    text preklada
    text theme_eng
    text title_eng
    text text_eng
    date imported_eng
    text theme_cz
    text title_cz
    text text_cz
    date changed_cz
  }
  table cactheme { key_id:'id_cactheme'
    number id_cactheme
    text url_theme
    text theme_eng
    text theme_cz
  }
  table _user {
    number id_user
    text username
  }
}
